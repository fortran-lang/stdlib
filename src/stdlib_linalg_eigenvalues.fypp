#:include "common.fypp"
module stdlib_linalg_eigenvalues
     use stdlib_linalg_constants
     use stdlib_linalg_blas
     use stdlib_linalg_lapack
     use stdlib_linalg_state
     use iso_fortran_env,only:real32,real64,real128,int8,int16,int32,int64,stderr => error_unit
     implicit none(type,external)
     private

     !> Eigendecomposition of a square matrix: return eigenvalues, and optionally eigenvectors
     public :: eig
     !> Eigenvalues of a square matrix
     public :: eigvals
     !> Eigendecomposition of a real symmetric or complex hermitian matrix
     public :: eigh
     !> Eigenvalues of a real symmetric or complex hermitian matrix
     public :: eigvalsh     

     ! Numpy: eigenvalues, eigenvectors = eig(a)
     !        eigenvalues = eigvals(a)
     ! Scipy: eig(a, b=None, left=False, right=True, overwrite_a=False, overwrite_b=False, check_finite=True, homogeneous_eigvals=False)

     ! Numpy: eigenvalues, eigenvectors = eigh(a, uplo='L')
     !        eigenvalues = eigvalsh(a)
     ! Scipy: eigh(a, b=None, *, lower=True, eigvals_only=False, overwrite_a=False, overwrite_b=False, turbo=<object object>, eigvals=<object object>, type=1, check_finite=True, subset_by_index=None, subset_by_value=None, driver=None)

     interface eig
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure stdlib_linalg_eig_${ri}$
        #:endfor
     end interface eig

     interface eigvals
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure stdlib_linalg_eigvals_${ri}$
        module procedure stdlib_linalg_eigvals_noerr_${ri}$
        #:endfor
     end interface eigvals
     
     interface eigh
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure stdlib_linalg_eigh_${ri}$
        #:endfor        
     end interface eigh
     
     interface eigvalsh
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure stdlib_linalg_eigvalsh_${ri}$
        module procedure stdlib_linalg_eigvalsh_noerr_${ri}$
        #:endfor                
     end interface eigvalsh


     character(*), parameter :: this = 'eigenvalues'


     contains
     
     !> Request for eigenvector calculation
     elemental character function eigenvectors_flag(required)
        logical(lk), intent(in) :: required
        eigenvectors_flag = merge('V','N',required)
     end function eigenvectors_flag
     
     !> Request for symmetry side (default: lower)
     elemental character function symmetric_triangle(upper)
        logical(lk), optional, intent(in) :: upper
        if (present(upper)) then 
           symmetric_triangle = merge('U','L',upper)
        else 
           symmetric_triangle = 'L'
        endif
     end function symmetric_triangle

     !> Process GEEV output flags
     elemental subroutine geev_info(err,info,m,n)
        !> Error handler
        type(linalg_state), intent(inout) :: err
        !> geev return flag
        integer(ilp), intent(in) :: info
        !> Input matrix size
        integer(ilp), intent(in) :: m,n

        select case (info)
           case (0)
               ! Success!
               err%state = LINALG_SUCCESS
           case (-1)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Invalid task ID: left eigenvectors.')
           case (-2)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Invalid task ID: right eigenvectors.')
           case (-5,-3)
               err = linalg_state(this,LINALG_VALUE_ERROR,'invalid matrix size: a=',[m,n])
           case (-9)
               err = linalg_state(this,LINALG_VALUE_ERROR,'insufficient left vector matrix size.')
           case (-11)
               err = linalg_state(this,LINALG_VALUE_ERROR,'insufficient right vector matrix size.')
           case (-13)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Insufficient work array size.')
           case (1:)
               err = linalg_state(this,LINALG_ERROR,'Eigenvalue computation did not converge.')
           case default
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Unknown error returned by geev.')
        end select

     end subroutine geev_info


     !> Process SYEV/HEEV output flags
     elemental subroutine heev_info(err,info,m,n)
        !> Error handler
        type(linalg_state), intent(inout) :: err
        !> geev return flag
        integer(ilp), intent(in) :: info
        !> Input matrix size
        integer(ilp), intent(in) :: m,n

        select case (info)
           case (0)
               ! Success!
               err%state = LINALG_SUCCESS
           case (-1)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Invalid eigenvector request.')
           case (-2)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Invalid triangular section request.')
           case (-5,-3)
               err = linalg_state(this,LINALG_VALUE_ERROR,'invalid matrix size: a=',[m,n])
           case (-8)
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'insufficient workspace size.')
           case (1:)
               err = linalg_state(this,LINALG_ERROR,'Eigenvalue computation did not converge.')
           case default
               err = linalg_state(this,LINALG_INTERNAL_ERROR,'Unknown error returned by syev/heev.')
        end select

     end subroutine heev_info

     #:for rk,rt,ri in ALL_KINDS_TYPES

     !> Return an array of eigenvalues of matrix A.
     function stdlib_linalg_eigvals_${ri}$(a,err) result(lambda)
         !> Input matrix A[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> [optional] state return flag. On error if not requested, the code will stop
         type(linalg_state), intent(out) :: err
         !> Array of singular values
         complex(${rk}$), allocatable :: lambda(:)

         !> Create
         ${rt}$, pointer :: amat(:,:)
         integer(ilp) :: m,n,k

         !> Create an internal pointer so the intent of A won't affect the next call
         amat => a

         m   = size(a,1,kind=ilp)
         n   = size(a,2,kind=ilp)
         k   = min(m,n)

         !> Allocate return storage
         allocate(lambda(k))

         !> Compute eigenvalues only
         call stdlib_linalg_eig_${ri}$(amat,lambda,overwrite_a=.false.,err=err)

     end function stdlib_linalg_eigvals_${ri}$

     !> Return an array of eigenvalues of matrix A.
     function stdlib_linalg_eigvals_noerr_${ri}$(a) result(lambda)
         !> Input matrix A[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> Array of singular values
         complex(${rk}$), allocatable :: lambda(:)

         !> Create
         ${rt}$, pointer :: amat(:,:)
         integer(ilp) :: m,n,k

         !> Create an internal pointer so the intent of A won't affect the next call
         amat => a

         m   = size(a,1,kind=ilp)
         n   = size(a,2,kind=ilp)
         k   = min(m,n)

         !> Allocate return storage
         allocate(lambda(k))

         !> Compute eigenvalues only
         call stdlib_linalg_eig_${ri}$(amat,lambda,overwrite_a=.false.)

     end function stdlib_linalg_eigvals_noerr_${ri}$

     !> Eigendecomposition of matrix A returning an array `lambda` of eigenvalues, 
     !> and optionally right or left eigenvectors.
     subroutine stdlib_linalg_eig_${ri}$(a,lambda,right,left,overwrite_a,err)
         !> Input matrix A[m,n]
         ${rt}$, intent(inout), target :: a(:,:)
         !> Array of eigenvalues
         complex(${rk}$), intent(out) :: lambda(:)
         !> The columns of RIGHT contain the right eigenvectors of A
         complex(${rk}$), optional, intent(out), target :: right(:,:)
         !> The columns of LEFT contain the left eigenvectors of A
         complex(${rk}$), optional, intent(out), target :: left(:,:)
         !> [optional] Can A data be overwritten and destroyed?
         logical(lk), optional, intent(in) :: overwrite_a
         !> [optional] state return flag. On error if not requested, the code will stop
         type(linalg_state), optional, intent(out) :: err

         !> Local variables
         type(linalg_state) :: err0
         integer(ilp) :: m,n,lda,ldu,ldv,info,k,lwork,lrwork,neig
         logical(lk) :: copy_a
         character :: task_u,task_v
         ${rt}$, target :: work_dummy(1),u_dummy(1,1),v_dummy(1,1)
         ${rt}$, allocatable :: work(:)
         real(${rk}$), allocatable :: rwork(:)
         ${rt}$, pointer :: amat(:,:),lreal(:),limag(:),umat(:,:),vmat(:,:)

         !> Matrix size
         m    = size(a,1,kind=ilp)
         n    = size(a,2,kind=ilp)
         k    = min(m,n)
         neig = size(lambda,kind=ilp) 
         lda  = m

         if (.not.(k>0 .and. m==n)) then
            err0 = linalg_state(this,LINALG_VALUE_ERROR,'invalid or matrix size a=',[m,n], &
                                                        ', must be square.')
            goto 1
         end if

         if (.not.neig>=k) then
            err0 = linalg_state(this,LINALG_VALUE_ERROR,'eigenvalue array has insufficient size:',&
                                                        ' lambda=',neig,', n=',n)
            goto 1
         endif



         ! Can A be overwritten? By default, do not overwrite
         if (present(overwrite_a)) then
            copy_a = .not.overwrite_a
         else
            copy_a = .true._lk
         endif

         ! Initialize a matrix temporary
         if (copy_a) then
            allocate(amat(m,n),source=a)
         else
            amat => a
         endif

         ! Decide if U, V eigenvectors
         task_u = eigenvectors_flag(present(left))
         task_v = eigenvectors_flag(present(right))         
         
         if (present(right)) then 
            
            #:if rt.startswith('complex')
            vmat => right
            #:else
            allocate(vmat(n,n))
            #:endif            
            
            if (size(vmat,2,kind=ilp)<n) then 
               err0 = linalg_state(this,LINALG_VALUE_ERROR,&
                                        'right eigenvector matrix has insufficient size: ',&
                                        shape(vmat),', with n=',n)
               goto 2
            endif  
            
         else
            vmat => v_dummy
         endif
            
         if (present(left)) then
            
            #:if rt.startswith('complex')
            umat => left
            #:else
            allocate(umat(n,n))
            #:endif
            
            if (size(umat,2,kind=ilp)<n) then 
               err0 = linalg_state(this,LINALG_VALUE_ERROR,&
                                        'left eigenvector matrix has insufficient size: ',&
                                        shape(umat),', with n=',n)
               goto 2
            endif            
            
         else
            umat => u_dummy
         endif

         ldu = size(umat,1,kind=ilp)
         ldv = size(vmat,1,kind=ilp)

         ! Compute workspace size
         #:if rt.startswith('complex')
         allocate(rwork(2*n))
         #:else
         allocate(lreal(n),limag(n))
         #:endif

         lwork = -1_ilp
        
         call geev(task_u,task_v,n,amat,lda,&
                   #{if rt.startswith('complex')}#lambda,#{else}#lreal,limag,#{endif}#  &
                   umat,ldu,vmat,ldv,&
                   work_dummy,lwork,#{if rt.startswith('complex')}#rwork,#{endif}#info)
         call geev_info(err0,info,m,n)

         ! Compute eigenvalues
         if (info==0) then

            !> Prepare working storage
            lwork = nint(real(work_dummy(1),kind=${rk}$), kind=ilp)
            allocate(work(lwork))

            !> Compute eigensystem
            call geev(task_u,task_v,n,amat,lda,&
                      #{if rt.startswith('complex')}#lambda,#{else}#lreal,limag,#{endif}#  &
                      umat,ldu,vmat,ldv,&            
                      work,lwork,#{if rt.startswith('complex')}#rwork,#{endif}#info)
            call geev_info(err0,info,m,n)

         endif
         
         ! Finalize storage and process output flag
         #:if not rt.startswith('complex')
         lambda(:n) = cmplx(lreal(:n),limag(:n),kind=${rk}$) 
         
         ! If the j-th and (j+1)-st eigenvalues form a complex conjugate pair, 
         ! geev returns reals as: 
         ! u(j)   = VL(:,j) + i*VL(:,j+1) and
         ! u(j+1) = VL(:,j) - i*VL(:,j+1). 
         ! Convert these to complex numbers here.            
         if (present(right)) call assign_real_eigenvectors_${rk}$(n,lambda,vmat,right)
         if (present(left))  call assign_real_eigenvectors_${rk}$(n,lambda,umat,left)
         #:endif
         
         2 if (copy_a) deallocate(amat)
         #:if not rt.startswith('complex')
         if (present(right)) deallocate(vmat)
         if (present(left)) deallocate(umat)
         #:endif           
         1 call linalg_error_handling(err0,err)

     end subroutine stdlib_linalg_eig_${ri}$
     
     !> Return an array of eigenvalues of real symmetric / complex hermitian A
     function stdlib_linalg_eigvalsh_${ri}$(a,upper_a,err) result(lambda)
         !> Input matrix A[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> [optional] Should the upper/lower half of A be used? Default: lower
         logical(lk), optional, intent(in) :: upper_a         
         !> [optional] state return flag. On error if not requested, the code will stop
         type(linalg_state), intent(out) :: err
         !> Array of singular values
         real(${rk}$), allocatable :: lambda(:)
         
         ${rt}$, pointer :: amat(:,:)
         integer(ilp) :: m,n,k

         !> Create an internal pointer so the intent of A won't affect the next call
         amat => a

         m   = size(a,1,kind=ilp)
         n   = size(a,2,kind=ilp)
         k   = min(m,n)

         !> Allocate return storage
         allocate(lambda(k))

         !> Compute eigenvalues only
         call stdlib_linalg_eigh_${ri}$(amat,lambda,upper_a=upper_a,overwrite_a=.false.,err=err)
         
     end function stdlib_linalg_eigvalsh_${ri}$
     
     !> Return an array of eigenvalues of real symmetric / complex hermitian A
     function stdlib_linalg_eigvalsh_noerr_${ri}$(a,upper_a) result(lambda)
         !> Input matrix A[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> [optional] Should the upper/lower half of A be used? Default: lower
         logical(lk), optional, intent(in) :: upper_a         
         !> Array of singular values
         real(${rk}$), allocatable :: lambda(:)

         ${rt}$, pointer :: amat(:,:)
         integer(ilp) :: m,n,k

         !> Create an internal pointer so the intent of A won't affect the next call
         amat => a

         m   = size(a,1,kind=ilp)
         n   = size(a,2,kind=ilp)
         k   = min(m,n)

         !> Allocate return storage
         allocate(lambda(k))

         !> Compute eigenvalues only
         call stdlib_linalg_eigh_${ri}$(amat,lambda,upper_a=upper_a,overwrite_a=.false.)

     end function stdlib_linalg_eigvalsh_noerr_${ri}$     

     !> Eigendecomposition of a real symmetric or complex Hermitian matrix A returning an array `lambda` 
     !> of eigenvalues, and optionally right or left eigenvectors.
     subroutine stdlib_linalg_eigh_${ri}$(a,lambda,vectors,upper_a,overwrite_a,err)
         !> Input matrix A[m,n]
         ${rt}$, intent(inout), target :: a(:,:)
         !> Array of eigenvalues
         real(${rk}$), intent(out) :: lambda(:)
         !> The columns of vectors contain the orthonormal eigenvectors of A
         ${rt}$, optional, intent(out), target :: vectors(:,:)
         !> [optional] Can A data be overwritten and destroyed?
         logical(lk), optional, intent(in) :: overwrite_a
         !> [optional] Should the upper/lower half of A be used? Default: lower
         logical(lk), optional, intent(in) :: upper_a
         !> [optional] state return flag. On error if not requested, the code will stop
         type(linalg_state), optional, intent(out) :: err

         !> Local variables
         type(linalg_state) :: err0
         integer(ilp) :: m,n,lda,info,k,lwork,neig
         logical(lk) :: copy_a
         character :: triangle,task
         ${rt}$, target :: work_dummy(1)
         ${rt}$, allocatable :: work(:)
         real(${rk}$), allocatable :: rwork(:)
         ${rt}$, pointer :: amat(:,:)

         !> Matrix size
         m    = size(a,1,kind=ilp)
         n    = size(a,2,kind=ilp)
         k    = min(m,n)
         neig = size(lambda,kind=ilp) 

         if (.not.(k>0 .and. m==n)) then
            err0 = linalg_state(this,LINALG_VALUE_ERROR,'invalid or matrix size a=',[m,n], &
                                                        ', must be square.')
            goto 1
         end if

         if (.not.neig>=k) then
            err0 = linalg_state(this,LINALG_VALUE_ERROR,'eigenvalue array has insufficient size:',&
                                                        ' lambda=',neig,' must be >= n=',n)
            goto 1
         endif
        
         ! Check if input A can be overwritten
         if (present(vectors)) then 
            ! No need to copy A anyways
            copy_a = .false.
         elseif (present(overwrite_a)) then
            copy_a = .not.overwrite_a
         else
            copy_a = .true._lk
         endif        
         
         ! Should we use the upper or lower half of the matrix?
         triangle = symmetric_triangle(upper_a)
         
         ! Request for eigenvectors
         task = eigenvectors_flag(present(vectors))
         
         if (present(vectors)) then 
            
            ! Check size
            if (any(shape(vectors,kind=ilp)<n)) then 
               err0 = linalg_state(this,LINALG_VALUE_ERROR,&
                                        'eigenvector matrix has insufficient size: ',&
                                        shape(vectors),', with n=',n)
               goto 1
            endif            
            
            ! The input matrix will be overwritten by the vectors. 
            ! So, use this one as storage for syev/heev
            amat => vectors
            
            ! Copy data in
            amat(:n,:n) = a(:n,:n)
                        
         elseif (copy_a) then
            ! Initialize a matrix temporary
            allocate(amat(m,n),source=a)
         else
            ! Overwrite A
            amat => a
         endif


         lda = size(amat,1,kind=ilp)

         ! Request workspace size
         lwork = -1_ilp
         #:if rt.startswith('complex')
         allocate(rwork(max(1,3*n-2)))
         call heev(task,triangle,n,amat,lda,lambda,work_dummy,lwork,rwork,info)
         #:else
         call syev(task,triangle,n,amat,lda,lambda,work_dummy,lwork,info)
         #:endif
         call heev_info(err0,info,m,n)

         ! Compute eigenvalues
         if (info==0) then

            !> Prepare working storage
            lwork = nint(real(work_dummy(1),kind=${rk}$), kind=ilp)
            allocate(work(lwork))

            !> Compute eigensystem
            #:if rt.startswith('complex')
            call heev(task,triangle,n,amat,lda,lambda,work,lwork,rwork,info)
            #:else
            call syev(task,triangle,n,amat,lda,lambda,work,lwork,info)
            #:endif
            call heev_info(err0,info,m,n)

         endif
         
         ! Finalize storage and process output flag         
         if (copy_a) deallocate(amat)
         1 call linalg_error_handling(err0,err)

     end subroutine stdlib_linalg_eigh_${ri}$
     
     #:endfor
     
     #:for rk,rt,ri in REAL_KINDS_TYPES
     !> GEEV for real matrices returns complex eigenvalues in real arrays. 
     !> Convert them to complex here, following the GEEV logic.
     pure subroutine assign_real_eigenvectors_${rk}$(n,lambda,lmat,out_mat)
        !> Problem size
        integer(ilp), intent(in) :: n
        !> Array of eigenvalues
        complex(${rk}$), intent(in) :: lambda(:)
        !> Real matrix as returned by geev 
        ${rt}$, intent(in) :: lmat(:,:)
        !> Complex matrix as returned by eig
        complex(${rk}$), intent(out) :: out_mat(:,:)
        
        integer(ilp) :: i,j
        
        ! Copy matrix
        do concurrent(i=1:n,j=1:n)
           out_mat(i,j) = lmat(i,j)
        end do
        
        ! If the j-th and (j+1)-st eigenvalues form a complex conjugate pair, 
        ! geev returns them as reals as: 
        ! u(j)   = VL(:,j) + i*VL(:,j+1) and
        ! u(j+1) = VL(:,j) - i*VL(:,j+1). 
        ! Convert these to complex numbers here.   
        do j=1,n-1
           if (lambda(j)==conjg(lambda(j+1))) then  
              out_mat(:,  j) = cmplx(lmat(:,j), lmat(:,j+1),kind=${rk}$)
              out_mat(:,j+1) = cmplx(lmat(:,j),-lmat(:,j+1),kind=${rk}$)
           endif
        end do           
        
     end subroutine assign_real_eigenvectors_${rk}$
     #:endfor


end module stdlib_linalg_eigenvalues
