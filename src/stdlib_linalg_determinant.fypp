#:include "common.fypp"
module stdlib_linalg_determinant
     use stdlib_linalg_constants
     use stdlib_linalg_blas
     use stdlib_linalg_lapack
     use stdlib_linalg_state
     use iso_fortran_env,only:real32,real64,real128,int8,int16,int32,int64,stderr => error_unit
     implicit none(type,external)
     private

     !> Determinant of a rectangular matrix
     public :: det

     character(*), parameter :: this = 'determinant'

     ! Numpy: det(a)
     ! Scipy: det(a, overwrite_a=False, check_finite=True)
     ! IMSL: DET(a)

     interface det
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure stdlib_linalg_${ri}$determinant
        #:endfor
     end interface det


     contains

     #:for rk,rt,ri in ALL_KINDS_TYPES
     ! Compute determinant of a square matrix A
     function stdlib_linalg_${ri}$determinant(a,overwrite_a,err) result(det)
         !> Input matrix a[m,n]
         ${rt}$, intent(inout), target :: a(:,:)
         !> [optional] Can A data be overwritten and destroyed?
         logical(lk), optional, intent(in) :: overwrite_a
         !> [optional] state return flag. On error if not requested, the code will stop
         type(linalg_state), optional, intent(out) :: err
         !> Result: matrix determinant
         ${rt}$ :: det

         !> Local variables
         type(linalg_state) :: err0
         integer(ilp) :: m,n,info,perm,k
         integer(ilp), allocatable :: ipiv(:)
         logical(lk) :: copy_a
         ${rt}$, pointer :: amat(:,:)

         !> Matrix determinant size
         m = size(a,1,kind=ilp)
         n = size(a,2,kind=ilp)

         if (m/=n .or. .not.min(m,n)>=0) then
            err0 = linalg_state(this,LINALG_VALUE_ERROR,'invalid or non-square matrix: a=[',m,',',n,']')
            det  = 0.0_${rk}$
            goto 1
         end if

         ! Can A be overwritten? By default, do not overwrite
         if (present(overwrite_a)) then
            copy_a = .not.overwrite_a
         else
            copy_a = .true._lk
         endif

         select case (m)
            case (0)
                ! Empty array has determinant 1 because math
                det = 1.0_${rk}$

            case (1)
                ! Scalar
                det = a(1,1)

            case default

                ! Find determinant from LU decomposition

                ! Initialize a matrix temporary
                if (copy_a) then
                   allocate(amat(m,n),source=a)
                else
                   amat => a
                endif

                ! Pivot indices
                allocate(ipiv(n))

                ! Compute determinant from LU factorization, then calculate the product of
                ! all diagonal entries of the U factor.
                call getrf(m,n,amat,m,ipiv,info)

                select case (info)
                   case (0)
                       ! Success: compute determinant

                       ! Start with real 1.0
                       det = 1.0_${rk}$
                       perm = 0
                       do k=1,n
                          if (ipiv(k)/=k) perm = perm+1
                          det = det*amat(k,k)
                       end do
                       if (mod(perm,2)/=0) det = -det

                   case (:-1)
                       err0 = linalg_state(this,LINALG_ERROR,'invalid matrix size a=[',m,',',n,']')
                   case (1:)
                       err0 = linalg_state(this,LINALG_ERROR,'singular matrix')
                   case default
                       err0 = linalg_state(this,LINALG_INTERNAL_ERROR,'catastrophic error')
                end select

                if (.not.copy_a) deallocate(amat)

         end select

         ! Process output and return
         1 call linalg_error_handling(err0,err)

     end function stdlib_linalg_${ri}$determinant

     #:endfor

end module stdlib_linalg_determinant
