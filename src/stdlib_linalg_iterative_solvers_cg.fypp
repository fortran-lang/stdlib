#:include "common.fypp"
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
#:set MATRIX_TYPES = ["dense", "CSR"]
#:set RANKS = range(1, 2+1)

submodule(stdlib_linalg_iterative_solvers) stdlib_linalg_iterative_cg
    use stdlib_kinds
    use stdlib_sparse
    use stdlib_constants
    use stdlib_linalg_iterative_solvers
    implicit none

contains

    #:for k, t, s in R_KINDS_TYPES
    module subroutine solve_cg_kernel_${s}$(A,b,x,tol,maxiter,workspace)
        class(linop_${s}$_type), intent(in) :: A
        ${t}$, intent(in) :: b(:), tol
        ${t}$, intent(inout) :: x(:)
        integer, intent(in) :: maxiter
        type(solver_workspace_${s}$_type), intent(inout) :: workspace
        !-------------------------
        integer :: iter
        ${t}$ :: norm_sq, norm_sq_old, norm_sq0
        ${t}$ :: alpha, beta, tolsq
        !-------------------------
        iter = 0
        associate(  P  => workspace%tmp(:,1), &
                    R  => workspace%tmp(:,2), &
                    Ap => workspace%tmp(:,3))
            
            norm_sq0 = A%inner_product(B, B)
            if(associated(workspace%callback)) call workspace%callback(x, norm_sq0, iter)

            R = B
            call A%apply(X, R, alpha= -one_${s}$, beta=one_${s}$) ! R = B - A*X
            norm_sq = A%inner_product(R, R)
   
            P = R
            
            tolsq = tol*tol
            beta = zero_${s}$
            if(associated(workspace%callback)) call workspace%callback(x, norm_sq, iter)
            do while( norm_sq > tolsq * norm_sq0 .and. iter < maxiter)
                call A%apply(P,Ap, alpha= one_${s}$, beta=zero_${s}$) ! Ap = A*P
                
                alpha = norm_sq / A%inner_product(P, Ap)
                
                X = X + alpha * P
                R = R - alpha * Ap

                norm_sq_old = norm_sq
                norm_sq = A%inner_product(R, R)
                beta = norm_sq / norm_sq_old
                
                P = R + beta * P

                iter = iter + 1

                if(associated(workspace%callback)) call workspace%callback(x, norm_sq, iter)
            end do
        end associate
    end subroutine
    #:endfor

    #:for matrix in MATRIX_TYPES
    #:for k, t, s in R_KINDS_TYPES
    module subroutine solve_cg_${matrix}$_${s}$(A,b,x,di,tol,maxiter,restart,workspace)
        #:if matrix == "dense"
        ${t}$, intent(in) :: A(:,:)
        #:else 
        type(${matrix}$_${s}$_type), intent(in) :: A
        #:endif
        ${t}$, intent(in) :: b(:)
        ${t}$, intent(inout) :: x(:)
        ${t}$, intent(in), optional :: tol
        logical(1), intent(in), optional, target  :: di(:)
        integer, intent(in), optional :: maxiter
        logical, intent(in), optional  :: restart
        type(solver_workspace_${s}$_type), optional, intent(inout), target :: workspace
        !-------------------------
        type(linop_${s}$_type) :: op
        type(solver_workspace_${s}$_type), pointer :: workspace_
        integer :: n, maxiter_
        ${t}$ :: tol_
        logical :: restart_
        logical(1), pointer :: di_(:)
        !-------------------------
        n = size(b)
        maxiter_ = n;       if(present(maxiter)) maxiter_ = maxiter
        restart_ = .true.;  if(present(restart)) restart_ = restart
        tol_ = 1.e-4_${s}$; if(present(tol)) tol_ = tol

        !-------------------------
        ! internal memory setup
        op%apply => default_matvec
        ! op%inner_product => default_dot
        if(present(di))then
            di_ => di
        else 
            allocate(di_(n),source=.false._1)
        end if
        
        if(present(workspace)) then
            workspace_ => workspace
        else
            allocate( workspace_ )
        end if
        if(.not.allocated(workspace_%tmp)) allocate( workspace_%tmp(n,size_wksp_cg), source = zero_${s}$ )
        !-------------------------
        ! main call to the solver
        if(restart_) x = zero_${s}$
        x = merge( b, x, di_ ) ! copy dirichlet load conditions encoded in B and indicated by di
        call solve_cg_kernel(op,b,x,tol_,maxiter_,workspace_)

        !-------------------------
        ! internal memory cleanup
        if(.not.present(di)) deallocate(di_)
        di_ => null()
        
        if(.not.present(workspace)) then
            deallocate( workspace_%tmp )
            deallocate( workspace_ )
        end if
        workspace_ => null()
        contains
        
        subroutine default_matvec(x,y,alpha,beta)
            ${t}$, intent(in)  :: x(:)
            ${t}$, intent(inout) :: y(:)
            ${t}$, intent(in) :: alpha
            ${t}$, intent(in) :: beta
            #:if matrix == "dense"
            y = alpha * matmul(A,x) + beta * y
            #:else 
            call spmv( A , x, y , alpha, beta )
            #:endif
            y = merge( zero_${s}$, y, di_ )
        end subroutine
    end subroutine

    #:endfor
    #:endfor

end submodule stdlib_linalg_iterative_cg