#:include "common.fypp"
#:set I_KINDS_TYPES = list(zip(INT_KINDS, INT_TYPES, INT_KINDS))
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))

module test_intrinsics
    use testdrive, only : new_unittest, unittest_type, error_type, check, skip_test
    use stdlib_kinds, only: sp, dp, xdp, qp, int8, int16, int32, int64
    use stdlib_intrinsics
    use stdlib_linalg_state, only: linalg_state_type, LINALG_VALUE_ERROR, operator(==)
    use stdlib_math, only: swap
    implicit none
    
contains

!> Collect all exported unit tests
subroutine collect_suite(testsuite)
    !> Collection of tests
    type(unittest_type), allocatable, intent(out) :: testsuite(:)

    testsuite = [ &
        new_unittest('sum', test_sum), &
        new_unittest('dot_product', test_dot_product), &
        new_unittest('matmul', test_matmul) &
    ]
end subroutine

subroutine test_sum(error)
    !> Error handling
    type(error_type), allocatable, intent(out) :: error

    !> Internal parameters and variables
    integer, parameter :: n = 1e3, ncalc = 3
    real(sp) :: u
    integer :: iter, i, j
    !====================================================================================
    #:for k, t, s in I_KINDS_TYPES
    #:if not k in ["int8","int16"] # skip int8 and int16
    block
        ${t}$, allocatable :: x(:)
        ${t}$, parameter :: total_sum = 0_${k}$
        ${t}$ :: xsum(ncalc), err(ncalc)
        logical, allocatable :: mask(:), nmask(:)

        allocate(x(n+1))
        do i = 1, n+1
            x(i) = i - n/2 - 1
        end do
        allocate(mask(n+1),source=.false.); mask(1:n+1:2) = .true.
        allocate(nmask(n+1)); nmask = .not.mask
        ! scramble array
        do i = 1, n+1
            call random_number(u) 
            j = 1 + floor(n*u)
            call swap( x(i), x(j) )
            call swap( mask(i), mask(j) )
            call swap( nmask(i), nmask(j) )
        end do
        
        xsum(1) = sum(x)        ! compiler intrinsic
        xsum(2) = stdlib_sum(x) ! chunked summation
        err(1:2) = abs(total_sum-xsum(1:2))
        
        call check(error, all(err(1:2)==0_${k}$) , "real sum is not accurate" )
        if (allocated(error)) return

        xsum(1) = sum(x,mask)+sum(x,nmask) ! compiler intrinsic
        xsum(2) = stdlib_sum(x,mask)+stdlib_sum(x,nmask) ! chunked summation
        err(1:2) = abs(total_sum-xsum(1:2))
        
        call check(error, all(err(1:2)==0_${k}$) , "masked real sum is not accurate" )
        if (allocated(error)) return
    end block
    #:endif
    #:endfor

    #:for k, t, s in R_KINDS_TYPES
    block
        ${t}$, allocatable :: x(:)
        ${t}$, parameter :: total_sum = 4*atan(1._${k}$), tolerance = epsilon(1._${k}$)*100
        ${t}$ :: xsum(ncalc), err(ncalc)
        logical, allocatable :: mask(:), nmask(:)

        allocate(x(n))
        do i = 1, n 
            x(i) = 8*atan(1._${k}$)*(real(i,kind=${k}$)-0.5_${k}$)/real(n,kind=${k}$)**2
        end do
        allocate(mask(n),source=.false.); mask(1:n:2) = .true.
        allocate(nmask(n)); nmask = .not.mask
        ! scramble array
        do i = 1, n
            call random_number(u) 
            j = 1 + floor(n*u)
            call swap( x(i), x(j) )
            call swap( mask(i), mask(j) )
            call swap( nmask(i), nmask(j) )
        end do
        
        xsum(1) = sum(x)        ! compiler intrinsic
        xsum(2) = stdlib_sum_kahan(x) ! chunked Kahan summation
        xsum(3) = stdlib_sum(x)       ! chunked summation
        err(1:ncalc) = abs(1._${k}$-xsum(1:ncalc)/total_sum)
        
        call check(error, all(err(:)<tolerance) , "real sum is not accurate" )
        if (allocated(error)) return

        xsum(1) = sum(x,mask)+sum(x,nmask) ! compiler intrinsic
        xsum(2) = stdlib_sum_kahan(x,mask)+stdlib_sum_kahan(x,nmask) ! chunked Kahan summation
        xsum(3) = stdlib_sum(x,mask)+stdlib_sum(x,nmask) ! chunked summation
        err(1:ncalc) = abs(1._${k}$-xsum(1:ncalc)/total_sum)
        
        call check(error, all(err(:)<tolerance) , "masked real sum is not accurate" )
        if (allocated(error)) return
    end block
    #:endfor

    #:for k, t, s in C_KINDS_TYPES
    block
        ${t}$, allocatable :: x(:)
        real(${k}$), parameter :: total_sum = 4*atan(1._${k}$), tolerance = epsilon(1._${k}$)*100
        real(${k}$) :: err(ncalc)
        ${t}$ :: xsum(ncalc)
        logical, allocatable :: mask(:), nmask(:)

        allocate(x(n))
        do i = 1, n
            x(i) = (8*atan(1._${k}$)*(real(i,kind=${k}$)-0.5_${k}$)/n**2)*cmplx(1._${k}$,1._${k}$)
        end do
        
        allocate(mask(n),source=.false.); mask(1:n:2) = .true.
        allocate(nmask(n)); nmask = .not.mask
        ! scramble array
        do i = 1, n
            call random_number(u) 
            j = 1 + floor(n*u)
            call swap( x(i), x(j) )
            call swap( mask(i), mask(j) )
            call swap( nmask(i), nmask(j) )
        end do
        
        xsum(1) = sum(x)        ! compiler intrinsic
        xsum(2) = stdlib_sum_kahan(x) ! chunked Kahan summation
        xsum(3) = stdlib_sum(x)       ! chunked summation
        err(1:ncalc) = abs(1._${k}$-(xsum(1:ncalc)%re)/total_sum)
        
        
        call check(error, all(err(:)<tolerance) , "complex sum is not accurate" )
        if (allocated(error)) return

        xsum(1) = sum(x,mask)+sum(x,nmask) ! compiler intrinsic
        xsum(2) = stdlib_sum_kahan(x,mask)+stdlib_sum_kahan(x,nmask) ! chunked Kahan summation
        xsum(3) = stdlib_sum(x,mask)+stdlib_sum(x,nmask) ! chunked summation
        err(1:ncalc) = abs(1._${k}$-(xsum(1:ncalc)%re)/total_sum)
        
        call check(error, all(err(:)<tolerance) , "complex masked sum is not accurate" )
        if (allocated(error)) return
    end block
    #:endfor

    ndarray : block
        use stdlib_strings, only: to_string
        real(sp), allocatable :: x(:,:,:)
        real(sp), parameter :: tolerance = epsilon(1._sp)*100
        integer :: i 

        allocate(x(100,100,10))
        call random_number(x)
        !> sum all elements
        call check(error, abs( sum(x) - stdlib_sum(x) )<tolerance*size(x) , "KO: full ndarray stdlib_sum" )
        if (allocated(error)) return

        call check(error, abs( sum(x) - stdlib_sum_kahan(x) )<tolerance*size(x) , "KO: full ndarray stdlib_sum_kahan" )
        if (allocated(error)) return

        !> sum over specific rank dim
        do i = 1, rank(x)
            call check(error, norm2( sum(x,dim=i) - stdlib_sum(x,dim=i) )<tolerance*size(x) ,&
                        "KO: ndarray stdlib_sum over dim "//to_string(i) )
            if (allocated(error)) return

            call check(error, norm2( sum(x,dim=i) - stdlib_sum_kahan(x,dim=i) )<tolerance*size(x) ,&
                        "KO: ndarray stdlib_sum_kahan over dim "//to_string(i) )
            if (allocated(error)) return
        end do
    end block ndarray

end subroutine

subroutine test_dot_product(error)
    !> Error handling
    type(error_type), allocatable, intent(out) :: error

    !> Internal parameters and variables
    integer, parameter :: n = 1e3, ncalc = 3
    real(sp) :: u
    integer :: iter, i, j
    !====================================================================================
    #:for k, t, s in R_KINDS_TYPES
    block
        ${t}$, allocatable :: x(:)
        ${t}$, parameter :: total_sum = 4*atan(1._${k}$), tolerance = epsilon(1._${k}$)*100
        ${t}$ :: xsum(ncalc), err(ncalc)

        allocate(x(n))
        do i = 1, n 
            x(i) = 2*sqrt( 2*atan(1._${k}$)*(real(i,kind=${k}$)-0.5_${k}$) )/n
        end do
        ! scramble array
        do i = 1, n
            call random_number(u) 
            j = 1 + floor(n*u)
            call swap( x(i), x(j) )
        end do
        
        xsum(1) = dot_product(x,x) ! compiler intrinsic
        xsum(2) = stdlib_dot_product_kahan(x,x) ! chunked Kahan summation
        xsum(3) = stdlib_dot_product(x,x)       ! chunked summation
        err(1:ncalc) = abs(1._${k}$-xsum(1:ncalc)/total_sum)
        
        call check(error, all(err(:)<tolerance) , "real dot_product is not accurate" )
        if (allocated(error)) return
    end block
    #:endfor

    #:for k, t, s in C_KINDS_TYPES
    block
        ${t}$, allocatable :: x(:)
        real(${k}$), parameter :: total_sum = 4*atan(1._${k}$), tolerance = epsilon(1._${k}$)*100
        real(${k}$) :: err(ncalc)
        ${t}$ :: xsum(ncalc)

        allocate(x(n))
        do i = 1, n
            x(i) = ( 2*sqrt( 2*atan(1._${k}$)*(real(i,kind=${k}$)-0.5_${k}$) ) / n )*cmplx(1._${k}$,1._${k}$)
        end do
        ! scramble array
        do i = 1, n
            call random_number(u) 
            j = 1 + floor(n*u)
            call swap( x(i), x(j) )
        end do
        
        xsum(1) = dot_product(x,x) ! compiler intrinsic
        xsum(2) = stdlib_dot_product_kahan(x,x) ! chunked Kahan dot_product
        xsum(3) = stdlib_dot_product(x,x)       ! chunked dot_product
        err(1:ncalc) = abs(1._${k}$-xsum(1:ncalc)%re/(2*total_sum))
         
        call check(error, all(err(:)<tolerance) , "complex dot_product is not accurate" )
        if (allocated(error)) return
    end block
    #:endfor

end subroutine

subroutine test_matmul(error)
    type(error_type), allocatable, intent(out) :: error
    type(linalg_state_type) :: linerr
    real :: a(2, 3), b(3, 4), c(3, 2), d(2, 2)

    d = stdlib_matmul(a, b, c, err=linerr)
    call check(error, linerr == LINALG_VALUE_ERROR, "incompatible matrices are considered compatible")
    if (allocated(error)) return

    #:for k, t, s in R_KINDS_TYPES
    block
        ${t}$ :: x(10,20), y(20,30), z(30,10), r(10,10), r1(10,10)
        call random_number(x)
        call random_number(y)
        call random_number(z)

        r = stdlib_matmul(x, y, z) ! the optimal ordering would be (x(yz))
        r1 = matmul(matmul(x, y), z) ! the opposite order to induce a difference

        call check(error, all(abs(r-r1) <= epsilon(0._${k}$) * 300), "real, ${k}$, 3 args: error too large")
        if (allocated(error)) return
    end block

    block
        ${t}$ :: x(10,20), y(20,30), z(30,10), w(10, 20), r(10,20), r1(10,20)
        call random_number(x)
        call random_number(y)
        call random_number(z)
        call random_number(w)

        r = stdlib_matmul(x, y, z, w) ! the optimal order would be ((x(yz))w)
        r1 = matmul(matmul(x, y), matmul(z, w))

        call check(error, all(abs(r-r1) <= epsilon(0._${k}$) * 1500), "real, ${k}$, 4 args: error too large")
        if (allocated(error)) return
    end block
    #:endfor
end subroutine test_matmul
    
end module test_intrinsics

program tester
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_intrinsics, only : collect_suite
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("sparse", collect_suite) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
