#:include "common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
! Test least squares solver
module test_linalg_least_squares
    use testdrive, only: error_type, check, new_unittest, unittest_type
    use stdlib_linalg_constants
    use stdlib_linalg, only: lstsq, solve_lstsq, generalized_lstsq, solve_generalized_lstsq, &
                             eigh, diag, eye, hermitian
    use stdlib_linalg_state, only: linalg_state_type

    implicit none (type,external)
    private
    
    public :: test_least_squares 

    contains

    !> Solve sample least squares problems
    subroutine test_least_squares(tests)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: tests(:)
        
        allocate(tests(0))
        
        call add_test(tests,new_unittest("issue_823",test_issue_823))

        #:for rk,rt,ri in REAL_KINDS_TYPES
        call add_test(tests,new_unittest("least_squares_${ri}$",test_lstsq_one_${ri}$))
        call add_test(tests,new_unittest("least_squares_randm_${ri}$",test_lstsq_random_${ri}$))
        call add_test(tests,new_unittest("generalized_lstsq_${ri}$",test_generalized_lstsq_${ri}$))
        call add_test(tests,new_unittest("generalized_lstsq_identity_${ri}$",test_generalized_lstsq_identity_${ri}$))
        call add_test(tests,new_unittest("generalized_lstsq_svd_sqrt_${ri}$",test_generalized_lstsq_svd_sqrt_${ri}$))
        call add_test(tests,new_unittest("generalized_lstsq_overwrite_${ri}$",test_generalized_lstsq_overwrite_${ri}$))
        call add_test(tests,new_unittest("solve_generalized_lstsq_${ri}$",test_solve_generalized_lstsq_${ri}$))
        #:endfor

    end subroutine test_least_squares
    
    #:for rk,rt,ri in REAL_KINDS_TYPES
    !> Simple polynomial fit
    subroutine test_lstsq_one_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp) :: rank

        !> Example scattered data
        ${rt}$, parameter :: x(*)  = real([1.0, 2.5, 3.5, 4.0, 5.0, 7.0, 8.5], ${rk}$)
        ${rt}$, parameter :: y(*)  = real([0.3, 1.1, 1.5, 2.0, 3.2, 6.6, 8.6], ${rk}$)
        ${rt}$, parameter :: ab(*) = real([0.20925829,  0.12013861], ${rk}$)

        ${rt}$ :: M(size(x),2),p(2)

        ! Coefficient matrix for polynomial y = a + b*x**2
        M(:,1) = x**0
        M(:,2) = x**2

        ! Find polynomial
        p = lstsq(M,y,rank=rank,err=state)

        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        call check(error, all(abs(p-ab)<1.0e-4_${rk}$), 'data converged')
        if (allocated(error)) return
        
        call check(error, rank==2, 'matrix rank == 2')
        if (allocated(error)) return

    end subroutine test_lstsq_one_${ri}$
    
    !> Fit from random array
    subroutine test_lstsq_random_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: n = 12, m = 3
        real :: Arnd(n,m),xrnd(m)
        ${rt}$, allocatable :: x(:)
        ${rt}$ :: xsol(m),y(n),A(n,m)

        ! Random coefficient matrix and solution
        call random_number(Arnd)
        call random_number(xrnd)
        
        ! Compute rhs
        A    = real(Arnd,${rk}$)
        xsol = real(xrnd,${rk}$)
        y    = matmul(A,xsol)

        ! Find polynomial
        x = lstsq(A,y,err=state)

        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        ! Check size
        call check(error,size(x)==m)
        if (allocated(error)) return
        
        call check(error, all(abs(x-xsol)<1.0e-4_${rk}$), 'data converged')
        if (allocated(error)) return
        
    end subroutine test_lstsq_random_${ri}$    
    
    #:endfor
    
    ! Test issue #823
    subroutine test_issue_823(error)
        type(error_type), allocatable, intent(out) :: error
        
        ! Dimension of the problem.
        integer(ilp), parameter :: n = 42
        ! Data for the least-squares problem.
        complex(dp) :: A(n+1, n), b(n+1), x_true(n), x_lstsq(n)
        ! Internal variables.
        real(dp), allocatable :: tmp(:, :, :), tmp_vec(:, :)
        ! Error handler
        type(linalg_state_type) :: state

        ! Zero-out data.
        A = 0.0_dp
        b = 0.0_dp
        x_lstsq = 0.0_dp
        allocate(tmp(n+1, n, 2), tmp_vec(n, 2), source=0.0_dp)

        ! Generate a random complex least-squares problem of size (n+1, n).
        call random_number(tmp)
        call random_number(tmp_vec)
        
        A      = cmplx(tmp(:, :, 1), tmp(:, :, 2), kind=dp)
        x_true = cmplx(tmp_vec(:, 1), tmp_vec(:, 2), kind=dp)
        b      = matmul(A, x_true)

        ! Solve the lstsq problem.
        call solve_lstsq(A, b, x_lstsq, err=state)
          
        ! Check that no segfault occurred
        call check(error,state%ok(),'issue 823 returned '//state%print())
        if (allocated(error)) return

        ! Check that least squares are verified
        call check(error,all(abs(x_true-x_lstsq)<sqrt(epsilon(0.0_dp))),'issue 823 results')
        if (allocated(error)) return

    end subroutine test_issue_823

    #:for rk,rt,ri in REAL_KINDS_TYPES
    !> Test basic generalized least-squares with correlated errors
    subroutine test_generalized_lstsq_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 3, n = 2
        ${rt}$ :: A(m,n), b(m), W(m,m)
        ${rt}$, allocatable :: x(:)

        ! Design matrix
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$]

        ! Observations
        b = [1.0_${rk}$, 2.1_${rk}$, 2.9_${rk}$]

        ! SPD covariance matrix (correlated errors)
        W(1,:) = [2.0_${rk}$, 1.0_${rk}$, 0.5_${rk}$]
        W(2,:) = [1.0_${rk}$, 2.0_${rk}$, 1.0_${rk}$]
        W(3,:) = [0.5_${rk}$, 1.0_${rk}$, 2.0_${rk}$]

        x = generalized_lstsq(W, A, b, err=state)

        call check(error, state%ok(), 'generalized_lstsq failed: '//state%print())
        if (allocated(error)) return

        call check(error, size(x)==n, 'generalized_lstsq: wrong solution size')
        if (allocated(error)) return

        ! Solution should be finite
        call check(error, all(abs(x) < huge(0.0_${rk}$)), 'generalized_lstsq: solution not finite')
        if (allocated(error)) return

    end subroutine test_generalized_lstsq_${ri}$

    !> Test GLS with identity covariance = OLS
    subroutine test_generalized_lstsq_identity_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 4, n = 2
        real(${rk}$), parameter :: tol = 1000*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), b(m), W(m,m)
        ${rt}$, allocatable :: x_gls(:), x_ols(:)

        ! Design matrix
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$, 4.0_${rk}$]
        b = [2.0_${rk}$, 4.0_${rk}$, 5.0_${rk}$, 4.0_${rk}$]

        ! Identity covariance
        W = eye(m, mold=0.0_${rk}$)

        x_gls = generalized_lstsq(W, A, b, err=state)
        call check(error, state%ok(), 'generalized_lstsq with I failed')
        if (allocated(error)) return

        x_ols = lstsq(A, b, err=state)
        call check(error, state%ok(), 'lstsq failed')
        if (allocated(error)) return

        ! GLS with identity should equal OLS
        call check(error, all(abs(x_gls - x_ols) < tol), &
                   'generalized_lstsq with identity should equal lstsq')
        if (allocated(error)) return

    end subroutine test_generalized_lstsq_identity_${ri}$

    !> Test GLS with eigendecomposition-based matrix square root (not Cholesky)
    !> This verifies that prefactored_w works with any valid matrix square root
    subroutine test_generalized_lstsq_svd_sqrt_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 3, n = 2
        real(${rk}$), parameter :: tol = 1000*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), b(m), W(m,m), sqrt_W(m,m), U(m,m)
        real(${rk}$) :: lambda(m)
        ${rt}$, allocatable :: x_chol(:), x_sqrt(:)

        ! Design matrix
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$]

        ! Observations
        b = [1.0_${rk}$, 2.1_${rk}$, 2.9_${rk}$]

        ! SPD covariance matrix
        W(1,:) = [2.0_${rk}$, 1.0_${rk}$, 0.5_${rk}$]
        W(2,:) = [1.0_${rk}$, 2.0_${rk}$, 1.0_${rk}$]
        W(3,:) = [0.5_${rk}$, 1.0_${rk}$, 2.0_${rk}$]

        ! Solve using Cholesky (internal factorization)
        x_chol = generalized_lstsq(W, A, b, err=state)
        call check(error, state%ok(), 'generalized_lstsq (Cholesky) failed: '//state%print())
        if (allocated(error)) return

        ! Compute eigendecomposition: W = U * diag(lambda) * U^T
        call eigh(W, lambda, vectors=U, err=state)
        call check(error, state%ok(), 'eigh failed: '//state%print())
        if (allocated(error)) return

        ! Compute matrix square root: sqrt(W) = U * diag(sqrt(lambda)) * U^H
        ! This is a DENSE matrix (not triangular like Cholesky factor)
        ! Using hermitian() instead of transpose() for complex-safety
        sqrt_W = matmul(U, matmul(diag(sqrt(lambda)), hermitian(U)))

        ! Solve using eigendecomposition-based square root (prefactored)
        x_sqrt = generalized_lstsq(sqrt_W, A, b, prefactored_w=.true., err=state)
        call check(error, state%ok(), 'generalized_lstsq (sqrt) failed: '//state%print())
        if (allocated(error)) return

        ! Both methods should give the same solution
        call check(error, all(abs(x_chol - x_sqrt) < tol), &
                   'generalized_lstsq: Cholesky and eigendecomposition-based sqrt should match')
        if (allocated(error)) return

    end subroutine test_generalized_lstsq_svd_sqrt_${ri}$

    !> Test GLS with overwrite_w=.true. (avoid internal allocation)
    subroutine test_generalized_lstsq_overwrite_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 3, n = 2
        real(${rk}$), parameter :: tol = 1000*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), b(m), W(m,m), W_copy(m,m)
        ${rt}$, allocatable :: x_copy(:), x_overwrite(:)

        ! Design matrix
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$]

        ! Observations
        b = [1.0_${rk}$, 2.1_${rk}$, 2.9_${rk}$]

        ! SPD covariance matrix
        W(1,:) = [2.0_${rk}$, 1.0_${rk}$, 0.5_${rk}$]
        W(2,:) = [1.0_${rk}$, 2.0_${rk}$, 1.0_${rk}$]
        W(3,:) = [0.5_${rk}$, 1.0_${rk}$, 2.0_${rk}$]
        W_copy = W

        ! Solve with copy (default)
        x_copy = generalized_lstsq(W_copy, A, b, err=state)
        call check(error, state%ok(), 'generalized_lstsq (copy) failed: '//state%print())
        if (allocated(error)) return

        ! Solve with overwrite (no internal allocation for W)
        x_overwrite = generalized_lstsq(W, A, b, overwrite_w=.true., err=state)
        call check(error, state%ok(), 'generalized_lstsq (overwrite) failed: '//state%print())
        if (allocated(error)) return

        ! Both should give the same solution
        call check(error, all(abs(x_copy - x_overwrite) < tol), &
                   'generalized_lstsq: copy and overwrite solutions should match')
        if (allocated(error)) return

    end subroutine test_generalized_lstsq_overwrite_${ri}$

    !> Test solve_generalized_lstsq matches generalized_lstsq
    subroutine test_solve_generalized_lstsq_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 4, n = 2
        real(${rk}$), parameter :: tol = 1000*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), b(m), W(m,m), x_sub(n)
        ${rt}$, allocatable :: x_fun(:)

        ! Design matrix
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$, 4.0_${rk}$]
        b = [2.0_${rk}$, 4.0_${rk}$, 5.0_${rk}$, 4.0_${rk}$]

        ! SPD covariance matrix
        W(1,:) = [2.0_${rk}$, 1.0_${rk}$, 0.5_${rk}$, 0.25_${rk}$]
        W(2,:) = [1.0_${rk}$, 2.0_${rk}$, 1.0_${rk}$, 0.5_${rk}$]
        W(3,:) = [0.5_${rk}$, 1.0_${rk}$, 2.0_${rk}$, 1.0_${rk}$]
        W(4,:) = [0.25_${rk}$, 0.5_${rk}$, 1.0_${rk}$, 2.0_${rk}$]

        ! Function form
        x_fun = generalized_lstsq(W, A, b, err=state)
        call check(error, state%ok(), 'generalized_lstsq (function) failed: '//state%print())
        if (allocated(error)) return

        ! Subroutine form
        call solve_generalized_lstsq(W, A, b, x_sub, err=state)
        call check(error, state%ok(), 'solve_generalized_lstsq failed: '//state%print())
        if (allocated(error)) return

        ! Both should give the same solution
        call check(error, all(abs(x_fun - x_sub) < tol), &
                   'solve_generalized_lstsq should match generalized_lstsq')
        if (allocated(error)) return

    end subroutine test_solve_generalized_lstsq_${ri}$

    #:endfor

    ! gcc-15 bugfix utility
    subroutine add_test(tests,new_test)
        type(unittest_type), allocatable, intent(inout) :: tests(:)    
        type(unittest_type), intent(in) :: new_test
        
        integer :: n
        type(unittest_type), allocatable :: new_tests(:)
        
        if (allocated(tests)) then 
            n = size(tests)
        else
            n = 0
        end if
        
        allocate(new_tests(n+1))
        if (n>0) new_tests(1:n) = tests(1:n)
                 new_tests(1+n) = new_test
        call move_alloc(from=new_tests,to=tests)        
        
    end subroutine add_test

end module test_linalg_least_squares

program test_lstsq
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_linalg_least_squares, only : test_least_squares
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("linalg_least_squares", test_least_squares) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program test_lstsq
