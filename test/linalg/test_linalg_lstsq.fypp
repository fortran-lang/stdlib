#:include "common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
! Test least squares solver
module test_linalg_least_squares
    use testdrive, only: error_type, check, new_unittest, unittest_type
    use stdlib_linalg_constants
    use stdlib_linalg, only: lstsq, solve_lstsq, weighted_lstsq
    use stdlib_linalg_state, only: linalg_state_type

    implicit none (type,external)
    private
    
    public :: test_least_squares 

    contains

    !> Solve sample least squares problems
    subroutine test_least_squares(tests)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: tests(:)
        
        allocate(tests(0))
        
        call add_test(tests,new_unittest("issue_823",test_issue_823))

        #:for rk,rt,ri in REAL_KINDS_TYPES
        call add_test(tests,new_unittest("least_squares_${ri}$",test_lstsq_one_${ri}$))
        call add_test(tests,new_unittest("least_squares_randm_${ri}$",test_lstsq_random_${ri}$))
        call add_test(tests,new_unittest("weighted_lstsq_${ri}$",test_weighted_lstsq_${ri}$))
        call add_test(tests,new_unittest("weighted_lstsq_effect_${ri}$",test_weighted_lstsq_effect_${ri}$))
        call add_test(tests,new_unittest("weighted_lstsq_negative_${ri}$",test_weighted_lstsq_negative_${ri}$))
        #:endfor

        #:for rk,rt,ri in CMPLX_KINDS_TYPES
        call add_test(tests,new_unittest("weighted_lstsq_${ri}$",test_weighted_lstsq_${ri}$))
        #:endfor

    end subroutine test_least_squares
    
    #:for rk,rt,ri in REAL_KINDS_TYPES
    !> Simple polynomial fit
    subroutine test_lstsq_one_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp) :: rank

        !> Example scattered data
        ${rt}$, parameter :: x(*)  = real([1.0, 2.5, 3.5, 4.0, 5.0, 7.0, 8.5], ${rk}$)
        ${rt}$, parameter :: y(*)  = real([0.3, 1.1, 1.5, 2.0, 3.2, 6.6, 8.6], ${rk}$)
        ${rt}$, parameter :: ab(*) = real([0.20925829,  0.12013861], ${rk}$)

        ${rt}$ :: M(size(x),2),p(2)

        ! Coefficient matrix for polynomial y = a + b*x**2
        M(:,1) = x**0
        M(:,2) = x**2

        ! Find polynomial
        p = lstsq(M,y,rank=rank,err=state)

        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        call check(error, all(abs(p-ab)<1.0e-4_${rk}$), 'data converged')
        if (allocated(error)) return
        
        call check(error, rank==2, 'matrix rank == 2')
        if (allocated(error)) return

    end subroutine test_lstsq_one_${ri}$
    
    !> Fit from random array
    subroutine test_lstsq_random_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: n = 12, m = 3
        real :: Arnd(n,m),xrnd(m)
        ${rt}$, allocatable :: x(:)
        ${rt}$ :: xsol(m),y(n),A(n,m)

        ! Random coefficient matrix and solution
        call random_number(Arnd)
        call random_number(xrnd)
        
        ! Compute rhs
        A    = real(Arnd,${rk}$)
        xsol = real(xrnd,${rk}$)
        y    = matmul(A,xsol)

        ! Find polynomial
        x = lstsq(A,y,err=state)

        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        ! Check size
        call check(error,size(x)==m)
        if (allocated(error)) return
        
        call check(error, all(abs(x-xsol)<1.0e-4_${rk}$), 'data converged')
        if (allocated(error)) return
        
    end subroutine test_lstsq_random_${ri}$    
    
    #:endfor

    !-------------------------------------------------------------
    !-----     Weighted Least-Squares Tests                  -----
    !-------------------------------------------------------------

    #:for rk,rt,ri in REAL_KINDS_TYPES
    !> Test basic weighted least-squares: uniform weights must match ordinary least-squares
    subroutine test_weighted_lstsq_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 4, n = 2
        real(${rk}$), parameter :: tol = 100*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), A_original(m,n), A_copy(m,n), b(m)
        real(${rk}$) :: w(m)
        ${rt}$, allocatable :: x_weighted(:), x_ols(:)

        ! Simple test case
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$, 4.0_${rk}$]
        b = [2.0_${rk}$, 4.0_${rk}$, 5.0_${rk}$, 4.0_${rk}$]
        w = 1.0_${rk}$  ! Uniform weights = OLS
        A_original = A  ! Save original to verify A is preserved
        A_copy = A      ! Save copy for lstsq comparison

        x_weighted = weighted_lstsq(w, A, b, err=state)

        call check(error, state%ok(), 'weighted_lstsq failed: '//state%print())
        if (allocated(error)) return
        
        call check(error, size(x_weighted)==n, 'weighted_lstsq: wrong solution size')
        if (allocated(error)) return

        ! Verify A is preserved by default (overwrite_a contract)
        call check(error, all(A == A_original), &
                   'weighted_lstsq must preserve A by default')
        if (allocated(error)) return

        ! KEY TEST: Uniform weights should match ordinary least squares exactly
        x_ols = lstsq(A_copy, b, err=state)

        call check(error, state%ok(), 'lstsq failed: '//state%print())
        if (allocated(error)) return

        call check(error, all(abs(x_weighted - x_ols) < tol), &
                   'weighted_lstsq with uniform weights must match lstsq')
        if (allocated(error)) return

    end subroutine test_weighted_lstsq_${ri}$

    !> Test that non-uniform weights change the solution
    subroutine test_weighted_lstsq_effect_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 4, n = 2
        real(${rk}$), parameter :: tol = 100*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), b(m)
        real(${rk}$) :: w_uniform(m), w_nonuniform(m)
        ${rt}$, allocatable :: x_uniform(:), x_weighted(:)

        ! Setup problem
        A(:,1) = 1.0_${rk}$
        A(:,2) = [1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$, 4.0_${rk}$]
        b = [1.0_${rk}$, 3.0_${rk}$, 2.0_${rk}$, 5.0_${rk}$]
        
        w_uniform = 1.0_${rk}$
        w_nonuniform = [10.0_${rk}$, 1.0_${rk}$, 1.0_${rk}$, 10.0_${rk}$]  ! Weight first and last more

        x_uniform = weighted_lstsq(w_uniform, A, b, err=state)
        call check(error, state%ok(), 'uniform weighted_lstsq failed')
        if (allocated(error)) return

        x_weighted = weighted_lstsq(w_nonuniform, A, b, err=state)
        call check(error, state%ok(), 'non-uniform weighted_lstsq failed')
        if (allocated(error)) return

        ! Solutions should be different
        call check(error, any(abs(x_uniform - x_weighted) > tol), &
                   'weighted_lstsq: non-uniform weights should change solution')
        if (allocated(error)) return

    end subroutine test_weighted_lstsq_effect_${ri}$

    !> Test error on negative weights
    subroutine test_weighted_lstsq_negative_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        ${rt}$ :: A(3,2), b(3)
        real(${rk}$) :: w(3)
        ${rt}$, allocatable :: x(:)

        A = 1.0_${rk}$
        b = 1.0_${rk}$
        w = [-1.0_${rk}$, 1.0_${rk}$, 1.0_${rk}$]  ! Invalid: negative weight!

        x = weighted_lstsq(w, A, b, err=state)

        call check(error, state%error(), 'weighted_lstsq should fail on negative weights')
        if (allocated(error)) return

    end subroutine test_weighted_lstsq_negative_${ri}$

    #:endfor

    #:for rk,rt,ri in CMPLX_KINDS_TYPES
    !> Test weighted least-squares with complex data: uniform weights must match lstsq
    subroutine test_weighted_lstsq_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state
        integer(ilp), parameter :: m = 4, n = 2
        real(${rk}$), parameter :: tol = 100*sqrt(epsilon(0.0_${rk}$))
        ${rt}$ :: A(m,n), A_copy(m,n), b(m)
        real(${rk}$) :: w(m)
        ${rt}$, allocatable :: x_weighted(:), x_ols(:)

        ! Complex test case
        A(:,1) = cmplx(1.0_${rk}$, 0.5_${rk}$, kind=${rk}$)
        A(:,2) = cmplx([1.0_${rk}$, 2.0_${rk}$, 3.0_${rk}$, 4.0_${rk}$], &
                       [0.1_${rk}$, 0.2_${rk}$, 0.3_${rk}$, 0.4_${rk}$], kind=${rk}$)
        b = cmplx([2.0_${rk}$, 4.0_${rk}$, 5.0_${rk}$, 4.0_${rk}$], &
                  [1.0_${rk}$, 1.5_${rk}$, 2.0_${rk}$, 1.0_${rk}$], kind=${rk}$)
        w = 1.0_${rk}$  ! Uniform weights = OLS (weights are always real)
        A_copy = A

        x_weighted = weighted_lstsq(w, A, b, err=state)

        call check(error, state%ok(), 'complex weighted_lstsq failed: '//state%print())
        if (allocated(error)) return
        
        call check(error, size(x_weighted)==n, 'complex weighted_lstsq: wrong solution size')
        if (allocated(error)) return

        ! KEY TEST: Uniform weights should match ordinary least squares
        x_ols = lstsq(A_copy, b, err=state)

        call check(error, state%ok(), 'complex lstsq failed: '//state%print())
        if (allocated(error)) return

        call check(error, all(abs(x_weighted - x_ols) < tol), &
                   'complex weighted_lstsq with uniform weights must match lstsq')
        if (allocated(error)) return

    end subroutine test_weighted_lstsq_${ri}$
    #:endfor
    
    ! Test issue #823
    subroutine test_issue_823(error)
        type(error_type), allocatable, intent(out) :: error
        
        ! Dimension of the problem.
        integer(ilp), parameter :: n = 42
        ! Data for the least-squares problem.
        complex(dp) :: A(n+1, n), b(n+1), x_true(n), x_lstsq(n)
        ! Internal variables.
        real(dp), allocatable :: tmp(:, :, :), tmp_vec(:, :)
        ! Error handler
        type(linalg_state_type) :: state

        ! Zero-out data.
        A = 0.0_dp
        b = 0.0_dp
        x_lstsq = 0.0_dp
        allocate(tmp(n+1, n, 2), tmp_vec(n, 2), source=0.0_dp)

        ! Generate a random complex least-squares problem of size (n+1, n).
        call random_number(tmp)
        call random_number(tmp_vec)
        
        A      = cmplx(tmp(:, :, 1), tmp(:, :, 2), kind=dp)
        x_true = cmplx(tmp_vec(:, 1), tmp_vec(:, 2), kind=dp)
        b      = matmul(A, x_true)

        ! Solve the lstsq problem.
        call solve_lstsq(A, b, x_lstsq, err=state)
          
        ! Check that no segfault occurred
        call check(error,state%ok(),'issue 823 returned '//state%print())
        if (allocated(error)) return

        ! Check that least squares are verified
        call check(error,all(abs(x_true-x_lstsq)<sqrt(epsilon(0.0_dp))),'issue 823 results')
        if (allocated(error)) return

    end subroutine test_issue_823

    ! gcc-15 bugfix utility
    subroutine add_test(tests,new_test)
        type(unittest_type), allocatable, intent(inout) :: tests(:)    
        type(unittest_type), intent(in) :: new_test
        
        integer :: n
        type(unittest_type), allocatable :: new_tests(:)
        
        if (allocated(tests)) then 
            n = size(tests)
        else
            n = 0
        end if
        
        allocate(new_tests(n+1))
        if (n>0) new_tests(1:n) = tests(1:n)
                 new_tests(1+n) = new_test
        call move_alloc(from=new_tests,to=tests)        
        
    end subroutine add_test

end module test_linalg_least_squares

program test_lstsq
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_linalg_least_squares, only : test_least_squares
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("linalg_least_squares", test_least_squares) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program test_lstsq
