#:include "common.fypp"
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
#:set KINDS_TYPES = R_KINDS_TYPES+C_KINDS_TYPES
module test_specialmatrices
    use testdrive, only : new_unittest, unittest_type, error_type, check, skip_test
    use stdlib_kinds
    use stdlib_constants
    use stdlib_linalg, only: hermitian
    use stdlib_linalg_state, only: linalg_state_type
    use stdlib_math, only: all_close
    use stdlib_specialmatrices
    implicit none

contains


    !> Collect all exported unit tests
    subroutine collect_suite(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest('tridiagonal', test_tridiagonal), &
            new_unittest('tridiagonal error handling', test_tridiagonal_error_handling), &
            new_unittest('symtridiagonal', test_symtridiagonal), &
            new_unittest('symtridiagonal error handling', test_symtridiagonal_error_handling) &
        ]
    end subroutine

    !----------------------------------------
    !-----                              -----
    !-----     TRIDIAGONAL MATRICES     -----
    !-----                              -----
    !----------------------------------------

    subroutine test_tridiagonal(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            integer, parameter :: n = 5
            type(tridiagonal_${s1}$_type) :: A
            ${t1}$, allocatable :: Amat(:,:), dl(:), dv(:), du(:)
            #: if t1.startswith('complex')
            real(wp), allocatable :: data(:, :)
            #:endif
            ${t1}$, allocatable :: x(:)
            ${t1}$, allocatable :: y1(:), y2(:)

            ! Initialize matrix.
            allocate(dl(n-1), dv(n), du(n-1))
            #:if t1.startswith('real')
            call random_number(dl) ; call random_number(dv) ; call random_number(du)
            #:else
            allocate(data(n, 2))
            call random_number(data) ; dl%re = data(:n-1, 1) ; dl%im = data(:n-1, 2)
            call random_number(data) ; dv%re = data(:n, 1) ; dv%im = data(:n, 2)
            call random_number(data) ; du%re = data(:n-1, 1) ; du%im = data(:n-1, 2)
            #:endif
            A = tridiagonal(dl, dv, du) ; Amat = dense(A)

            ! Random vectors.
            #:if t1.startswith('real')
            allocate(x(n), source = 0.0_wp)   ; call random_number(x)
            allocate(y1(n), source = 0.0_wp)  ; allocate(y2(n), source=0.0_wp)
            #:else
            allocate(x(n), source=zero_c${k1}$)
            call random_number(data) ; x%re = data(:, 1) ; x%im = data(:, 2)
            allocate(y1(n), source = zero_c${k1}$)  ; allocate(y2(n), source=zero_c${k1}$)
            #:endif

            ! Test y = A @ x
            y1 = matmul(Amat, x) ; call spmv(A, x, y2)
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return

            ! Test y = A.T @ x
            y1 = 0.0_wp ; y2 = 0.0_wp
            y1 = matmul(transpose(Amat), x) ; call spmv(A, x, y2, op="T")
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return

            #:if t1.startswith('complex') 
            ! Test y = A.H @ x
            y1 = 0.0_wp ; y2 = 0.0_wp
            y1 = matmul(hermitian(Amat), x) ; call spmv(A, x, y2, op="H")
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return
            #:endif
        end block
        #:endfor
    end subroutine

    subroutine test_tridiagonal_error_handling(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            integer, parameter :: n = 5
            type(tridiagonal_${s1}$_type) :: A
            ${t1}$, allocatable :: dl(:), dv(:), du(:)
            type(linalg_state_type) :: state
            integer :: i

            !> Test constructor from arrays.
            dl = [(1.0_wp, i = 1, n-2)] ; du = dl
            dv = [(2.0_wp, i = 1, n)]
            A = tridiagonal(dl, dv, du, state)
            call check(error, state%ok(), .false.)
            if (allocated(error)) return

            !> Test contructor from constants.
            A = tridiagonal(dl(1), dv(1), du(1), -n, state)
            call check(error, state%ok(), .false.)
            if (allocated(error)) return
        end block
        #:endfor       
    end subroutine

    !--------------------------------------------------
    !-----                                        -----
    !-----     SYMMETRIC TRIDIAGONAL MATRICES     -----
    !-----                                        -----
    !--------------------------------------------------

    subroutine test_symtridiagonal(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            integer, parameter :: n = 5
            type(symtridiagonal_${s1}$_type) :: A
            ${t1}$, allocatable :: Amat(:,:), dv(:), ev(:)
            #:if t1.startswith('complex')
            real(wp), allocatable :: data(:, :)
            #:endif
            ${t1}$, allocatable :: x(:)
            ${t1}$, allocatable :: y1(:), y2(:)

            ! Initialize matrix.
            allocate(ev(n-1), dv(n))
            #:if t1.startswith('real')
            call random_number(dv) ; call random_number(ev)
            #:else
            allocate(data(n, 2), source=0.0_wp)
            call random_number(data) ; dv%re = data(:, 1) ; dv%im = data(:, 2)
            call random_number(data) ; ev%re = data(:n-1, 1) ; ev%im = data(:n-1, 2)
            #:endif
            A = symtridiagonal(dv, ev) ; Amat = dense(A)

            ! Random vectors.
            #:if t1.startswith('real')
            allocate(x(n), source = 0.0_wp)   ; call random_number(x)
            allocate(y1(n), source = 0.0_wp)  ; allocate(y2(n), source=0.0_wp)
            #:else
            allocate(x(n), source=zero_c${k1}$)
            call random_number(data) ; x%re = data(:, 1) ; x%im = data(:, 2)
            allocate(y1(n), source = zero_c${k1}$)  ; allocate(y2(n), source=zero_c${k1}$)
            #:endif

            ! Test y = A @ x
            y1 = matmul(Amat, x) ; call spmv(A, x, y2)
            print *, "matvec :", all_close(y1, y2)
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return

            ! Test y = A.T @ x
            y1 = 0.0_wp ; y2 = 0.0_wp
            y1 = matmul(transpose(Amat), x) ; call spmv(A, x, y2, op="T")
            print *, "tranposed matvec :", all_close(y1, y2)
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return

            #:if t1.startswith('complex') 
            ! Test y = A.H @ x
            y1 = 0.0_wp ; y2 = 0.0_wp
            y1 = matmul(hermitian(Amat), x) ; call spmv(A, x, y2, op="H")
            print *, "hermitian matvec :", all_close(y1, y2)
            call check(error, all_close(y1, y2), .true.)
            if (allocated(error)) return
            #:endif
        end block
        #:endfor
    end subroutine

    subroutine test_symtridiagonal_error_handling(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            integer, parameter :: n = 5
            type(symtridiagonal_${s1}$_type) :: A
            ${t1}$, allocatable :: dv(:), ev(:)
            type(linalg_state_type) :: state
            integer :: i

            !> Test constructor from arrays.
            ev = [(1.0_wp, i = 1, n-2)]
            dv = [(2.0_wp, i = 1, n)]
            A = symtridiagonal(dv, ev, state)
            call check(error, state%ok(), .false.)
            if (allocated(error)) return

            !> Test contructor from constants.
            A = symtridiagonal(dv(1), ev(1), -n, state)
            call check(error, state%ok(), .false.)
            if (allocated(error)) return
        end block
        #:endfor       
    end subroutine

end module


program tester
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_specialmatrices, only : collect_suite
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("sparse", collect_suite) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
