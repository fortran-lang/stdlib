#:include "common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
! Test inverse matrix operator
module test_linalg_inverse
    use testdrive, only: error_type, check, new_unittest, unittest_type
    use stdlib_linalg_constants
    use stdlib_linalg, only: inv,invert,operator(.inv.),eye
    use stdlib_linalg_state, only: linalg_state_type,LINALG_ERROR

    implicit none (type,external)
    private
    
    public :: test_inverse_matrix

    contains

    !> Matrix inversion tests
    subroutine test_inverse_matrix(tests)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: tests(:)
        
        allocate(tests(0))

        #:for rk,rt,ri in RC_KINDS_TYPES
        tests = [tests,new_unittest("${ri}$_eye_inverse",test_${ri}$_eye_inverse), &
                       new_unittest("${ri}$_singular_inverse",test_${ri}$_singular_inverse), &
                       new_unittest("${ri}$_random_spd_inverse",test_${ri}$_random_spd_inverse)]
        #:endfor

    end subroutine test_inverse_matrix

    #:for rk,rt,ri in REAL_KINDS_TYPES
    !> Invert real identity matrix
    subroutine test_${ri}$_eye_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state

        integer(ilp), parameter :: n = 25_ilp
        ${rt}$ :: a(n,n),inva(n,n)

        a = eye(n)

        !> Inverse function
        inva  = inv(a,err=state)
        call check(error,state%ok(),'inverse_${ri}$_eye (function): '//state%print())
        if (allocated(error)) return
        
        call check(error,all(abs(a-inva)<epsilon(0.0_${rk}$)),'inverse_${ri}$_eye (function): data converged')
        if (allocated(error)) return  
        
        !> Inverse subroutine: split
        call invert(a,inva,err=state)

        call check(error,state%ok(),'inverse_${ri}$_eye (subroutine): '//state%print())
        if (allocated(error)) return
        
        call check(error,all(abs(a-inva)<epsilon(0.0_${rk}$)),'inverse_${ri}$_eye (subroutine): data converged')
        if (allocated(error)) return   

        !> Inverse subroutine in-place
        call invert(a,err=state)

        call check(error,state%ok(),'inverse_${ri}$_eye (in-place): '//state%print())
        if (allocated(error)) return
        
        call check(error,all(abs(a-inva)<epsilon(0.0_${rk}$)),'inverse_${ri}$_eye (in-place): data converged')
        if (allocated(error)) return     

    end subroutine test_${ri}$_eye_inverse

    !> Invert singular matrix
    subroutine test_${ri}$_singular_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: err

        integer(ilp), parameter :: n = 25_ilp
        ${rt}$ :: a(n,n)

        a = eye(n)
        
        !> Make rank-deficient
        a(12,12) = 0
        
        !> Inverse function
        call invert(a,err=err)
        call check(error,err%state==LINALG_ERROR,'singular ${rt}$ inverse returned '//err%print())
        if (allocated(error)) return
        
    end subroutine test_${ri}$_singular_inverse
    
    !> Create a random symmetric positive definite matrix
    function random_spd_matrix_${ri}$(n) result(A)
        integer(ilp), intent(in) :: n
        ${rt}$ :: A(n,n)
        
        ${rt}$, parameter :: one  = 1.0_${rk}$
        ${rt}$, parameter :: half = 0.5_${rk}$
        
        !> Initialize with randoms
        call random_number(A)
        
        !> Make symmetric
        A = half*(A+transpose(A))
        
        !> Add diagonally dominant part 
        A = A + n*eye(n)
        
    end function random_spd_matrix_${ri}$

    !> Test random symmetric positive-definite matrix
    subroutine test_${ri}$_random_spd_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        !> Solution tolerance
        ${rt}$, parameter :: tol = sqrt(epsilon(0.0_${rk}$))

        !> Local variables
        integer(ilp), parameter :: n = 5_ilp
        type(linalg_state_type) :: state
        ${rt}$ :: A(n,n),Am1(n,n)
        
        !> Generate random SPD matrix
        A = random_spd_matrix_${ri}$(n)

        !> Invert matrix
        call invert(A,Am1,err=state)
        
        !> Check result    
        call check(error,state%ok(),'random SPD matrix (${rk}$): '//state%print())
        if (allocated(error)) return     

        call check(error,all(abs(matmul(Am1,A)-eye(n))<tol),'random SPD matrix (${rk}$): accuracy test')
        if (allocated(error)) return    
    
    end subroutine test_${ri}$_random_spd_inverse
    
    #:endfor

    !> Invert complex identity matrix
    #:for ck,ct,ci in CMPLX_KINDS_TYPES
    subroutine test_${ci}$_eye_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state

        integer(ilp) :: i,j,failed
        integer(ilp), parameter :: n = 25_ilp

        ${ct}$ :: a(n,n),copya(n,n),inva(n,n)

        do concurrent (i=1:n,j=1:n)
          a(i,j) = merge((1.0_${ck}$,1.0_${ck}$),(0.0_${ck}$,0.0_${ck}$),i==j)
        end do
        copya = a

        !> The inverse of a complex diagonal matrix has conjg(z_ii)/abs(z_ii)^2 on the diagonal
        inva = inv(a,err=state)

        call check(error,state%ok(),'inverse_${ci}$_eye (function): '//state%print())
        if (allocated(error)) return
        
        failed = 0
        do i=1,n
            do j=1,n
                if (.not.is_diagonal_inverse(a(i,j),inva(i,j),i,j)) failed = failed+1
            end do
        end do

        call check(error,failed==0,'inverse_${ci}$_eye (function): data converged')
        if (allocated(error)) return     

        !> Inverse subroutine
        call invert(copya,err=state)

        call check(error,state%ok(),'inverse_${ci}$_eye (subroutine): '//state%print())
        if (allocated(error)) return

        failed = 0
        do i=1,n
            do j=1,n
                if (.not.is_diagonal_inverse(a(i,j),copya(i,j),i,j)) failed = failed+1
            end do
        end do

        call check(error,failed==0,'inverse_${ci}$_eye (subroutine): data converged')
        if (allocated(error)) return   

        contains

           elemental logical function is_diagonal_inverse(aij,invaij,i,j)
               ${ct}$, intent(in) :: aij,invaij
               integer(ilp), intent(in) :: i,j
               if (i/=j) then
                  is_diagonal_inverse = max(abs(aij),abs(invaij))<epsilon(0.0_${ck}$)
               else
                  ! Product should return the real identity
                  is_diagonal_inverse = abs(aij*invaij - (1.0_${ck}$,0.0_${ck}$))<epsilon(0.0_${ck}$)
               endif
           end function is_diagonal_inverse

    end subroutine test_${ci}$_eye_inverse

    !> Create a random symmetric positive definite matrix
    function random_spd_matrix_${ci}$(n) result(A)
        integer(ilp), intent(in) :: n
        ${ct}$ :: A(n,n)
        
        ${ct}$, parameter :: half = (0.5_${ck}$,0.0_${ck}$)
        real(${ck}$) :: reA(n,n),imA(n,n)
        integer(ilp) :: i
        
        !> Initialize with randoms
        call random_number(reA)
        call random_number(imA)
        
        A = cmplx(reA,imA,kind=${ck}$)
        
        !> Make symmetric
        A = half*(A+transpose(A))
        
        !> Add diagonally dominant part 
        forall(i=1:n) A(i,i) = A(i,i) + n*(1.0_${ck}$,0.0_${ck}$)
        
    end function random_spd_matrix_${ci}$

    !> Test random symmetric positive-definite matrix
    subroutine test_${ci}$_random_spd_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        !> Local variables
        integer(ilp) :: failed,i,j
        integer(ilp), parameter :: n = 5_ilp
        type(linalg_state_type) :: state
        ${ct}$ :: A(n,n),Am1(n,n),AA(n,n)
        
        !> Generate random SPD matrix
        A = random_spd_matrix_${ci}$(n)

        !> Invert matrix
        call invert(A,Am1,err=state)
        
        !> Check result    
        call check(error,state%ok(),'random complex SPD matrix (${ck}$): '//state%print())
        if (allocated(error)) return     

        failed = 0
        AA = matmul(A,Am1)
        do i=1,n
            do j=1,n
                if (.not.is_complex_inverse(AA(i,j),i,j)) failed = failed+1
            end do
        end do

        call check(error,failed==0,'inverse_${ci}$_eye (subroutine): data converged')
        if (allocated(error)) return   

        contains

           elemental logical function is_complex_inverse(aij,i,j)
               ${ct}$, intent(in) :: aij
               integer(ilp), intent(in) :: i,j
               real(${ck}$), parameter :: tol = sqrt(epsilon(0.0_${ck}$))
               if (i/=j) then
                  is_complex_inverse = abs(aij)<tol
               else
                  ! Product should return the real identity
                  is_complex_inverse = abs(aij - (1.0_${ck}$,0.0_${ck}$))<tol
               endif
           end function is_complex_inverse
    
    end subroutine test_${ci}$_random_spd_inverse

    !> Invert singular matrix
    subroutine test_${ci}$_singular_inverse(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: err

        integer(ilp), parameter :: n = 25_ilp
        ${ct}$ :: a(n,n)

        a = (0.0_${ck}$,0.0_${ck}$)
        
        !> Inverse function
        call invert(a,err=err)
        call check(error,err%state==LINALG_ERROR,'singular ${ct}$ inverse returned '//err%print())
        if (allocated(error)) return
        
    end subroutine test_${ci}$_singular_inverse

    #:endfor

end module test_linalg_inverse

program test_inv
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_linalg_inverse, only : test_inverse_matrix
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("linalg_inverse", test_inverse_matrix) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program test_inv
