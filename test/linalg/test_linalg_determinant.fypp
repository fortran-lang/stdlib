#:include "common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
! Test matrix determinant
module test_linalg_determinant
    use testdrive, only: error_type, check, new_unittest, unittest_type
    use stdlib_linalg_constants
    use stdlib_linalg, only: eye, det, linalg_state_type

    implicit none (type,external)
    private
    
    public :: test_matrix_determinant 

    contains
    

    !> Matrix inversion tests
    subroutine test_matrix_determinant(tests)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: tests(:)
        
        allocate(tests(0))

        #:for rk,rt in RC_KINDS_TYPES
        #:if rk!="xdp"
        tests = [tests,new_unittest("$eye_det_${rt[0]}$${rk}$",test_${rt[0]}$${rk}$_eye_determinant)]
        tests = [tests,new_unittest("$eye_det_multiple_${rt[0]}$${rk}$",test_${rt[0]}$${rk}$_eye_multiple)]
        #:endif
        #:endfor
        #:for ck,ct in CMPLX_KINDS_TYPES
        #:if ck!="xdp"
        tests = [tests,new_unittest("$complex_det_${rt[0]}$${rk}$",test_${ct[0]}$${ck}$_complex_determinant)]
        #:endif
        #: endfor

    end subroutine test_matrix_determinant

    #:for rk,rt in RC_KINDS_TYPES
    #:if rk!="xdp"
    !> Determinant of identity matrix
    subroutine test_${rt[0]}$${rk}$_eye_determinant(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state

        integer(ilp) :: i
        integer(ilp), parameter :: n = 128_ilp

        ${rt}$ :: a(n,n),deta
        ${rt}$, allocatable :: aalloc(:,:)

        a = eye(n)

        !> Determinant function
        deta = det(a,err=state)
        
        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        call check(error, abs(deta-1.0_${rk}$)<epsilon(0.0_${rk}$), 'det(eye(n))==1')
        if (allocated(error)) return

        !> Test with allocatable matrix
        aalloc = eye(n)
        deta = det(aalloc,overwrite_a=.false.,err=state)
        call check(error,state%ok(),state%print()//' (allocatable a)')
        if (allocated(error)) return
        call check(error,allocated(aalloc),'a is still allocated')
        if (allocated(error)) return
        call check(error, abs(deta-1.0_${rk}$)<epsilon(0.0_${rk}$), 'det(eye(n))==1 (allocatable a))')
        if (allocated(error)) return
        
    end subroutine test_${rt[0]}$${rk}$_eye_determinant

    !> Determinant of identity matrix multiplier
    subroutine test_${rt[0]}$${rk}$_eye_multiple(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state

        integer(ilp), parameter :: n = 4_ilp
        real(${rk}$), parameter :: coef = 0.01_${rk}$
        integer(ilp) :: i,j
        ${rt}$ :: a(n,n),deta

        !> Multiply eye by a very small number
        a = eye(n)
        do concurrent (i=1:n)
          a(i,i) = coef
        end do

        !> Determinant: small, but a is not singular, because it is a multiple of the identity.
        deta = det(a,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        call check(error, abs(deta-coef**n)<max(tiny(0.0_${rk}$),epsilon(0.0_${rk}$)*coef**n), &
                         'det(0.01*eye(n))==0.01^n')

    end subroutine test_${rt[0]}$${rk}$_eye_multiple
    #:endif
    #:endfor

    !> Determinant of complex identity matrix
    #:for ck,ct in CMPLX_KINDS_TYPES
    #:if ck!="xdp"
    subroutine test_${ct[0]}$${ck}$_complex_determinant(error)
        type(error_type), allocatable, intent(out) :: error

        type(linalg_state_type) :: state

        integer(ilp) :: i,j,n
        integer(ilp), parameter :: nmax = 10_ilp

        ${ct}$, parameter :: res(nmax) = [${ct}$::(1,1),(0,2),(-2,2),(-4,0),(-4,-4), &
                                                  (0,-8),(8,-8),(16,0),(16,16),(0,32)]

        ${ct}$, allocatable :: a(:,:)
        ${ct}$ :: deta(nmax)

        !> Test determinant for all sizes, 1:nmax
        matrix_size: do n=1,nmax

           ! Put 1+i on each diagonal element
           a = eye(n)
           do concurrent (i=1:n)
             a(i,i) = (1.0_${ck}$,1.0_${ck}$)
           end do

           ! Expected result
           deta(n) = det(a,err=state)

           deallocate(a)
           if (state%error()) exit matrix_size

        end do matrix_size

        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        call check(error, all(abs(res-deta)<=epsilon(0.0_${ck}$)), &
                         'det((1+i)*eye(n))  does not match result')

    end subroutine test_${ct[0]}$${ck}$_complex_determinant

    #:endif
    #:endfor

end module test_linalg_determinant

program test_det
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_linalg_determinant, only : test_matrix_determinant
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("linalg_determinant", test_matrix_determinant) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
