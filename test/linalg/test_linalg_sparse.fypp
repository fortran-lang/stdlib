#:include "common.fypp"
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set KINDS_TYPES = R_KINDS_TYPES
module test_sparse_spmv
    use testdrive, only : new_unittest, unittest_type, error_type, check, skip_test
    use stdlib_kinds, only: sp, dp, xdp, qp, int8, int16, int32, int64
    use stdlib_sparse

    implicit none

contains


    !> Collect all exported unit tests
    subroutine collect_suite(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest('coo', test_coo), &
            new_unittest('coo2ordered', test_coo2ordered), &
            new_unittest('csr', test_csr), &
            new_unittest('csc', test_csc), &
            new_unittest('ell', test_ell),  &
            new_unittest('sellc', test_sellc),  &
            new_unittest('symmetries', test_symmetries), &
            new_unittest('diagonal', test_diagonal), &
            new_unittest('add_get_values', test_add_get_values), &
            new_unittest('add_block_symmetric_skip', test_add_block_symmetric_skip) &
        ]
    end subroutine

    subroutine test_coo(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(COO_${s1}$_type) :: COO
            ${t1}$, allocatable :: dense(:,:)
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y1(:), vec_y2(:)

            allocate( dense(4,5) , source = &
                    reshape(real([9,4, 0,4, &
                                  0,7, 8,0, &
                                  0,0,-1,5, &
                                  0,0, 8,6, &
                                 -3,0, 0,0],kind=wp),[4,5]) )
            
            call dense2coo( dense , COO )
            
            allocate( vec_x(5)  , source = 1._wp )
            allocate( vec_y1(4) , source = 0._wp )
            allocate( vec_y2(4) , source = 0._wp )

            vec_y1 = matmul( dense, vec_x )
            
            call check(error, all(vec_y1 == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return

            call spmv( COO, vec_x, vec_y2 )
            call check(error, all(vec_y1 == vec_y2) )
            if (allocated(error)) return

            ! Test in-place transpose
            vec_y1 = 1._wp
            call spmv( COO, vec_y1, vec_x, op=sparse_op_transpose )
            call check(error, all(vec_x == real([17,15,4,14,-3],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_coo2ordered(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        type(COO_sp_type) :: COO
        integer :: row(12), col(12)
        real    :: data(12)

        row = [1,1,1,2,2,3,2,2,2,3,3,4]
        col = [2,3,4,3,4,4,3,4,5,4,5,5]
        data = 1.0

        call from_ijv(COO,row,col,data)

        call coo2ordered(COO,sort_data=.true.)
        call check(error, COO%nnz < 12 .and. COO%nnz == 9 )
        if (allocated(error)) return

        call check(error, all(COO%data==[1,1,1,2,2,1,2,1,1])  )
        if (allocated(error)) return

        call check(error, all(COO%index(1,:)==[1,1,1,2,2,2,3,3,4])  )
        if (allocated(error)) return

        call check(error, all(COO%index(2,:)==[2,3,4,3,4,5,4,5,5])  )
        if (allocated(error)) return

    end subroutine 

    subroutine test_csr(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(CSR_${s1}$_type) :: CSR
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call CSR%malloc(4,5,10)
            CSR%data(:)   = real([9,-3,4,7,8,-1,8,4,5,6],kind=wp)
            CSR%col(:)    = [1,5,1,2,2,3,4,1,3,4]
            CSR%rowptr(:) = [1,3,5,8,11]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call spmv( CSR, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return

            ! Test in-place transpose
            vec_y = 1._wp
            call spmv( CSR, vec_y, vec_x, op=sparse_op_transpose )
            call check(error, all(vec_x == real([17,15,4,14,-3],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_csc(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(CSC_${s1}$_type) :: CSC
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call CSC%malloc(4,5,10)
            CSC%data(:)   = real([9,4,4,7,8,-1,5,8,6,-3],kind=wp)
            CSC%row(:)    = [1,2,4,2,3,3,4,3,4,1]
            CSC%colptr(:) = [1,4,6,8,10,11]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call spmv( CSC, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return

            ! Test in-place transpose
            vec_y = 1._wp
            call spmv( CSC, vec_y, vec_x, op=sparse_op_transpose )
            call check(error, all(vec_x == real([17,15,4,14,-3],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_ell(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(ELL_${s1}$_type) :: ELL
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call ELL%malloc(4,5,3)
            ELL%data(1,1:3)   = real([9,-3,0],kind=wp)
            ELL%data(2,1:3)   = real([4,7,0],kind=wp)
            ELL%data(3,1:3)   = real([8,-1,8],kind=wp)
            ELL%data(4,1:3)   = real([4,5,6],kind=wp)

            ELL%index(1,1:3) = [1,5,0]
            ELL%index(2,1:3) = [1,2,0]
            ELL%index(3,1:3) = [2,3,4]
            ELL%index(4,1:3) = [1,3,4]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call spmv( ELL, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return

            ! Test in-place transpose
            vec_y = 1._wp
            call spmv( ELL, vec_y, vec_x, op=sparse_op_transpose )
            call check(error, all(vec_x == real([17,15,4,14,-3],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
        
    end subroutine

    subroutine test_sellc(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(SELLC_${s1}$_type) :: SELLC
            type(CSR_${s1}$_type)   :: CSR
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:), vec_y2(:)
            integer :: i

            call CSR%malloc(6,6,17)
            !           1   2   3   4   5   6 
            CSR%col = [ 1,      3,  4,         &
                            2,  3,      5,  6, &
                        1,  2,  3,             &
                                        5,  6, &
                                    4,  5,     &
                            2,          5,  6]
            CSR%rowptr = [1,4,8,11,13,15,18]
            CSR%data = [(real(i,kind=wp),i=1,CSR%nnz)] 
            
            call csr2sellc(CSR,SELLC,4)

            allocate( vec_x(6) , source = 1._wp )
            allocate( vec_y(6) , source = 0._wp )
            
            call spmv( SELLC, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,22,27,23,27,48],kind=wp)) )
            if (allocated(error)) return

            ! Test in-place transpose
            vec_x = real( [1,2,3,4,5,6] , kind=wp )
            call spmv( CSR, vec_x, vec_y , op = sparse_op_transpose )
            allocate( vec_y2(6) , source = 0._wp )
            call spmv( SELLC, vec_x, vec_y2 , op = sparse_op_transpose )
            
            call check(error, all(vec_y == vec_y2))
            if (allocated(error)) return

        end block
        #:endfor
    end subroutine

    subroutine test_symmetries(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(COO_${s1}$_type) :: COO
            type(CSR_${s1}$_type) :: CSR
            type(ELL_${s1}$_type) :: ELL
            ${t1}$, allocatable :: dense(:,:)
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y1(:), vec_y2(:), vec_y3(:), vec_y4(:)

            allocate( vec_x(4)  , source = 1._wp )
            allocate( vec_y1(4) , source = 0._wp )
            allocate( vec_y2(4) , source = 0._wp )
            allocate( vec_y3(4) , source = 0._wp )
            allocate( vec_y4(4) , source = 0._wp )

            allocate( dense(4,4) , source = &
                    reshape(real([1,0,0,0, &
                                  2,1,0,0, &
                                  0,2,1,0,&
                                  0,0,2,1],kind=wp),[4,4]) )

            call dense2coo( dense , COO )
            COO%storage = sparse_upper
            call coo2csr(COO, CSR)
            call csr2ell(CSR, ELL)

            dense(2,1) = 2._wp; dense(3,2) = 2._wp; dense(4,3) = 2._wp
            vec_y1 = matmul( dense, vec_x )
            call check(error, all(vec_y1 == [3,5,5,3]) )
            if (allocated(error)) return

            call spmv( COO , vec_x, vec_y2 )
            call check(error, all(vec_y1 == vec_y2) )
            if (allocated(error)) return

            call spmv( CSR , vec_x, vec_y3 )
            call check(error, all(vec_y1 == vec_y3) )
            if (allocated(error)) return

            call spmv( ELL , vec_x, vec_y4 )
            call check(error, all(vec_y1 == vec_y4) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_diagonal(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            ${t1}$, allocatable :: dense(:,:)
            type(COO_${s1}$_type) :: COO
            type(CSR_${s1}$_type) :: CSR
            type(CSC_${s1}$_type) :: CSC
            ${t1}$, allocatable :: diagonal(:)

            allocate( dense(4,4) , source = &
                    reshape(real([1,0,0,5, &
                                  0,2,0,0, &
                                  0,6,3,0,&
                                  0,0,7,4],kind=wp),[4,4]) )

            call diag(dense,diagonal)
            call check(error, all(diagonal == [1,2,3,4]) )
            if (allocated(error)) return

            diagonal = 0.0
            call dense2coo( dense , COO )
            call diag( COO , diagonal )
            call check(error, all(diagonal == [1,2,3,4]) )
            if (allocated(error)) return

            diagonal = 0.0
            call coo2csr( COO, CSR )
            call diag( CSR , diagonal )
            call check(error, all(diagonal == [1,2,3,4]) )
            if (allocated(error)) return

            diagonal = 0.0
            call coo2csc( COO, CSC )
            call diag( CSC , diagonal )
            call check(error, all(diagonal == [1,2,3,4]) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_add_get_values(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            real(wp) :: dense(5,5), mat(2,2)
            type(COO_${s1}$_type) :: COO
            type(CSR_${s1}$_type) :: CSR
            ${t1}$:: err
            integer :: i, j, locdof(2)

            mat(:,1) = [1,2]; mat(:,2) = [2,1]
            dense = 0._wp
            do i = 0, 3
                dense(1+i:2+i,1+i:2+i) = dense(1+i:2+i,1+i:2+i) + mat
            end do

            call dense2coo(dense,COO)
            call coo2csr(COO,CSR)

            CSR%data = 0._wp

            do i = 0, 3
                locdof(1:2) = [1+i,2+i] 
                call CSR%add(locdof,locdof,mat)
            end do

            call check(error, all(CSR%data == COO%data) )
            if (allocated(error)) return
            err = 0._wp
            do i = 1, 5
                do j = 1, 5
                    err = err + abs(dense(i,j) - CSR%at(i,j))
                end do
            end do
            err = err / 5*5

            call check(error, err <= epsilon(0._wp) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_add_block_symmetric_skip(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            integer :: connectivity(3,3)

            real(wp) :: dense(5,5), dense_low(5,5), mat(3,3)
            type(COO_${s1}$_type) :: COO_full, COO_low
            type(CSR_${s1}$_type) :: CSR_full, CSR_low
            type(CSC_${s1}$_type) :: CSC_full, CSC_low
            real(wp)  :: x(5), y(5), y_ref(5)
            ${t1}$:: err
            integer :: i, j, locdof(3)

            connectivity(1:3,1) = [1,2,3]
            connectivity(1:3,2) = [2,3,4]
            connectivity(1:3,3) = [3,4,5]

            mat(:,1) = [1,2,3]
            mat(:,2) = [2,1,4]
            mat(:,3) = [3,4,1]

            dense = 0._wp
            do i = 1, 3
                locdof(1:3) = connectivity(1:3,i)
                dense(locdof,locdof) = dense(locdof,locdof) + mat
            end do

            call dense2coo(dense,COO_full)
            call coo2csr(COO_full,CSR_full)
            call coo2csc(COO_full,CSC_full)
            dense_low = dense
            do i = 1, 5
                do j = i+1, 5
                    dense_low(i,j) = 0._wp
                end do
            end do
            call dense2coo(dense_low,COO_low)
            COO_low%storage = sparse_lower
            call coo2csr(COO_low,CSR_low)
            call coo2csc(COO_low,CSC_low)

            COO_full%data = 0._wp
            COO_low%data = 0._wp
            CSR_full%data = 0._wp
            CSR_low%data = 0._wp
            CSC_full%data = 0._wp
            CSC_low%data = 0._wp
            do i = 1, 3
                locdof(1:3) = connectivity(1:3,i)
                call COO_full%add(locdof,locdof,mat)
                call COO_low%add(locdof,locdof,mat)
                call CSR_full%add(locdof,locdof,mat)
                call CSR_low%add(locdof,locdof,mat)
                call CSC_full%add(locdof,locdof,mat)
                call CSC_low%add(locdof,locdof,mat)
            end do

            call check(error, all(CSR_full%data == COO_full%data) , "error in full CSR ${s1}$ data" )
            if (allocated(error)) return

            call check(error, all(CSR_low%data == COO_low%data) , "error in low CSR ${s1}$ data" )
            if (allocated(error)) return

            x = 1._wp
            y_ref = matmul(dense,x)

            y = 0._wp
            call spmv( CSR_full, x, y )
            call check(error, all(y == y_ref) , "error in full CSR ${s1}$ spmv" )
            if (allocated(error)) return
            
            y = 0._wp
            call spmv( CSR_low, x, y ) 
            call check(error, all(y == y_ref) , "error in low CSR ${s1}$ spmv" )
            if (allocated(error)) return

            y = 0._wp
            call spmv( CSC_full, x, y )
            call check(error, all(y == y_ref) , "error in full CSC ${s1}$ spmv" )
            if (allocated(error)) return
            
            y = 0._wp
            call spmv( CSC_low, x, y ) 
            call check(error, all(y == y_ref) , "error in low CSC ${s1}$ spmv" )
            if (allocated(error)) return

        end block
        #:endfor
    end subroutine
end module


program tester
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_sparse_spmv, only : collect_suite
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("sparse", collect_suite) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
