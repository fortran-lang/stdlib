#:include "common.fypp"
#:set RC_KINDS_TYPES = REAL_KINDS_TYPES + CMPLX_KINDS_TYPES
! Test Schur decomposition
module test_linalg_schur
    use testdrive, only: error_type, check, new_unittest, unittest_type
    use stdlib_linalg_constants
    use stdlib_linalg_state, only: LINALG_VALUE_ERROR,linalg_state_type
    use stdlib_linalg, only: schur,schur_space
    use ieee_arithmetic, only: ieee_value,ieee_quiet_nan

    implicit none (type,external)
    
    public :: test_schur_decomposition

    contains

    !> schur decomposition tests
    subroutine test_schur_decomposition(tests)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: tests(:)
        
        allocate(tests(0))
        
        #:for rk,rt,ri in RC_KINDS_TYPES
        tests = [tests,new_unittest("schur_api_${ri}$",test_schur_api_${ri}$), &
                       new_unittest("schur_random_${ri}$",test_schur_random_${ri}$), &
                       new_unittest("schur_symmetric_${ri}$",test_schur_symmetric_${ri}$)]
        #:endfor

    end subroutine test_schur_decomposition

    !> schur decomposition of a random matrix
    #:for rk,rt,ri in RC_KINDS_TYPES
    subroutine test_schur_api_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        integer(ilp), parameter :: n   = 15_ilp
        integer(ilp) :: lwork
        real(${rk}$), parameter :: tol = 10*sqrt(epsilon(0.0_${rk}$))
        complex(${rk}$) :: eigs(n)
        ${rt}$, dimension(n,n) :: a,t,z
        ${rt}$, allocatable :: storage(:)
        #:if 'complex' in rt
        real(${rk}$) :: rea(n,n),ima(n,n)
        #:endif
        type(linalg_state_type) :: state
        
        #:if 'complex' in rt
        call random_number(rea)
        call random_number(ima)
        a = cmplx(rea,ima,kind=${rk}$)
        #:else
        call random_number(a)
        #:endif
        
        ! Test simple API
        call schur(a,t,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return      
        
        ! Test output transformation matrix
        call schur(a,t,z,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return      

        ! Test output eigenvalues
        call schur(a,t,eigvals=eigs,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return      
        
        ! Test storage query
        call schur_space(a,lwork,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return      
        
        ! Test with user-defined storage
        allocate(storage(lwork))        
        call schur(a,t,eigvals=eigs,storage=storage,err=state)
        call check(error,state%ok(),state%print())
        if (allocated(error)) return      
        
    end subroutine test_schur_api_${ri}$    
    
    subroutine test_schur_random_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        integer(ilp), parameter :: n = 3_ilp
        real(${rk}$), parameter :: rtol = 1.0e-4_${rk}$
        real(${rk}$), parameter :: eps  = sqrt(epsilon(0.0_${rk}$))
        integer(ilp) :: lwork
        ${rt}$, allocatable :: storage(:)
        ${rt}$, dimension(n,n) :: a,t,z,aorig
        #:if 'complex' in rt
        real(${rk}$), dimension(n,n) :: a_re,a_im
        #:endif        
        type(linalg_state_type) :: state

        #:if 'complex' in rt
        call random_number(a_re)
        call random_number(a_im)
        a = cmplx(a_re,a_im,kind=${rk}$)
        #:else
        call random_number(a)
        #:endif
        aorig = a

        ! 1) Run schur (standard)
        call schur(a,t,z,err=state)
        
        ! Check return code
        call check(error,state%ok(),state%print())
        if (allocated(error)) return            
        
        ! Check solution
        call check(error, all(schur_error(a,z,t)<=max(rtol*abs(a),eps)), &
                          'converged solution (${rt}$)')
        if (allocated(error)) return             

        ! 2) Run schur (overwrite A)
        call schur(a,t,z,overwrite_a=.true.,err=state)

        ! Check return code
        call check(error,state%ok(),state%print())
        if (allocated(error)) return            
        
        ! Check solution
        call check(error, all(schur_error(aorig,z,t)<=max(rtol*abs(aorig),eps)), &
                          'converged solution (${rt}$ - overwrite A)')
        if (allocated(error)) return            
        
        ! 3) Use working storage
        a = aorig   
        call schur_space(a,lwork,err=state)
         
        ! Check return code
        call check(error,state%ok(),state%print())
        if (allocated(error)) return
        
        allocate(storage(lwork))         
        call schur(a,t,z,storage=storage,err=state)
        
        ! Check return code
        call check(error,state%ok(),state%print())
        if (allocated(error)) return            
        
        ! Check solution
        call check(error, all(schur_error(a,z,t)<=max(rtol*abs(a),eps)), &
                          'converged solution (${rt}$ - external storage)')
        if (allocated(error)) return            

    contains
    
        pure function schur_error(a,z,t) result(err)
            ${rt}$, intent(in), dimension(:,:) :: a,z,t
            real(${rk}$), dimension(size(a,1),size(a,2)) :: err
            
            #:if rt.startswith('real')
            err = abs(matmul(matmul(z,t),transpose(z)) - a)
            #:else
            err = abs(matmul(matmul(z,t),conjg(transpose(z))) - a)
            #:endif                
        end function schur_error
        
    end subroutine test_schur_random_${ri}$

    !> Test symmetric matrix (real eigenvalues)
    subroutine test_schur_symmetric_${ri}$(error)
        type(error_type), allocatable, intent(out) :: error

        integer(ilp), parameter :: n = 3_ilp
        real(${rk}$), parameter :: rtol = 1.0e-4_${rk}$
        real(${rk}$), parameter :: eps  = sqrt(epsilon(0.0_${rk}$))
        real(${rk}$) :: reigs(n)
        ${rt}$, dimension(n,n) :: a, t, z
        type(linalg_state_type) :: state

        ! Define a symmetric 3x3 matrix with real eigenvalues
        a = reshape([ 3, 1, 0, &
                      1, 3, 1, &
                      0, 1, 3], shape=[n, n])

        ! Return real eigenvalues (Should trigger an error if they have an imaginary part)
        call schur(a, t, z, eigvals=reigs, err=state)
        
        ! Check return code
        call check(error, state%ok(), state%print())
        if (allocated(error)) return

        ! Check solution
        call check(error, all(schur_error(a, z, t) <= max(rtol * abs(a), eps)), &
                          'converged solution (real symmetric, real eigs)')
        if (allocated(error)) return

    contains
    
        pure function schur_error(a,z,t) result(err)
            ${rt}$, intent(in), dimension(:,:) :: a,z,t
            real(${rk}$), dimension(size(a,1),size(a,2)) :: err
            
            #:if rt.startswith('real')
            err = abs(matmul(matmul(z,t),transpose(z)) - a)
            #:else
            err = abs(matmul(matmul(z,t),conjg(transpose(z))) - a)
            #:endif                
        end function schur_error        

    end subroutine test_schur_symmetric_${ri}$


    #:endfor

end module test_linalg_schur

program test_schur
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_linalg_schur, only : test_schur_decomposition
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("linalg_schur", test_schur_decomposition) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program test_schur
