#:include "common.fypp"
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
module test_kabsch
    use testdrive, only : new_unittest, unittest_type, error_type, check, skip_test
    use stdlib_kinds
    use stdlib_math, only: all_close, is_close
    use stdlib_linalg, only: svd
    use stdlib_spatial
    implicit none

contains


    !> Collect all exported unit tests
    subroutine collect_suite(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest('kabsch_real', test_kabsch_real), &
            new_unittest('kabsch_complex', test_kabsch_complex) &
        ]
    end subroutine

    subroutine test_kabsch_real(error)
        type(error_type), allocatable, intent(out) :: error
        #:for k, t, s in (R_KINDS_TYPES)
        block
            integer, parameter :: d = 3, N = 4
            ${t}$ :: P_original(d, N), Q_original(d, N), P_recovered(d, N)
            ${t}$ :: R_recovered(d, d), R_original(d, d)
            ${t}$ :: t_recovered(d), t_original(d)
            ${t}$ :: U(d,d), Vt(d,d), S(d)
            ${t}$ :: c_recovered, c_original
            ${t}$ :: rmsd

            integer :: i, j
            ${t}$ :: r1

            call random_seed()

            ! Random reference points Q
            call random_number(Q_original)

            ! Random proper rotation matrix R_original
            ! Constructed via SVD: R = U * V^T
            call random_number(R_original)
            call svd(R_original, S, U, Vt)
            R_original = matmul(U, Vt)

            ! Random scale and translation
            call random_number(c_original)
            call random_number(t_original)

            ! Construct P = c*R*Q + t
            do j = 1, N
                P_original(:,j) = c_original * matmul(R_original, Q_original(:,j)) + t_original
            end do

            ! Call Kabsch–Umeyama
            call kabsch(P_original, Q_original, R_recovered, t_recovered, c_recovered, rmsd)

            ! Apply recovered transform
            do j = 1, N
                P_recovered(:,j) = c_recovered * matmul(R_recovered, Q_original(:,j)) + t_recovered
            end do

            call check(error, all_close(P_recovered, P_original), .true.)
            if (allocated(error)) return
            call check(error, all_close(R_recovered, R_original), .true.)
            if (allocated(error)) return
            call check(error, is_close(c_recovered, c_original), .true.)
            if (allocated(error)) return
            call check(error, all_close(t_recovered, t_original), .true.)
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_kabsch_complex(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k, t, s in (C_KINDS_TYPES)
        block
            integer, parameter :: d = 3, N = 4
            ${t}$ :: P_original(d, N), Q_original(d, N), P_recovered(d, N)
            ${t}$ :: R_recovered(d, d), R_original(d, d)
            ${t}$ :: t_recovered(d), t_original(d)
            ${t}$ :: c_recovered, c_original
            ${t}$ :: U(d,d), Vt(d,d)
            real(${k}$) :: S(d)
            real(${k}$) :: rmsd

            integer :: i, j
            real(${k}$) :: r1, r2

            call random_seed()

            ! Random complex reference points Q
            do j = 1, N
                do i = 1, d
                    call random_number(r1)
                    call random_number(r2)
                    Q_original(i,j) = cmplx(r1, r2, kind=${k}$)
                end do
            end do

            ! Random complex unitary matrix R_original
            ! Constructed via SVD: R = U * V^H
            do i = 1, d
                do j = 1, d
                    call random_number(r1)
                    call random_number(r2)
                    R_original(i,j) = cmplx(r1, r2, kind=${k}$)
                end do
            end do
            call svd(R_original, S, U, Vt)
            R_original = matmul(U, Vt)

            ! Random scale and translation
            call random_number(r1)
            call random_number(r2)
            c_original = cmplx(r1,r2, kind=${k}$)

            do i = 1, d
                call random_number(r1)
                call random_number(r2)
                t_original(i) = cmplx(r1, r2, kind=${k}$)
            end do

            ! Construct P = c*R*Q + t
            do j = 1, N
                P_original(:,j) = c_original * matmul(R_original, Q_original(:,j)) + t_original
            end do

            ! Call Kabsch–Umeyama
            call kabsch(P_original, Q_original, R_recovered, t_recovered, c_recovered, rmsd)

            ! Apply recovered transform
            do j = 1, N
                P_recovered(:,j) = c_recovered * matmul(R_recovered, Q_original(:,j)) + t_recovered
            end do
            call check(error, all_close(P_recovered, P_original), .true.)
            if (allocated(error)) return
            call check(error, all_close(c_recovered * R_recovered, c_original * R_original), .true.)
            if (allocated(error)) return
            call check(error, all_close(t_recovered, t_original), .true.)
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

end module


program tester
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_kabsch, only : collect_suite
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("kabsch", collect_suite) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
