#:include "common.fypp"
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
module test_kabsch_umeyama
    use testdrive, only : new_unittest, unittest_type, error_type, check, skip_test
    use stdlib_kinds
    use stdlib_math, only: all_close, is_close
    use stdlib_linalg, only: svd
    use stdlib_spatial
    use stdlib_intrinsics, only: stdlib_dot_product_kahan
    use stdlib_constants
    implicit none

contains


    !> Collect all exported unit tests
    subroutine collect_suite(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest('kabsch_umeyama_real', test_kabsch_umeyama_real), &
            new_unittest('kabsch_umeyama_complex', test_kabsch_umeyama_complex) &
        ]
    end subroutine

    subroutine test_kabsch_umeyama_real(error)
        type(error_type), allocatable, intent(out) :: error
        #:for k, t, s in (R_KINDS_TYPES)
        block
            integer, parameter :: d = 3, N = 8
            ${t}$ :: P_original(d, N), P_recovered(d, N), Q_original(d, N)
            ${t}$ :: R_recovered(d, d), R_original(d, d)
            ${t}$ :: t_recovered(d), t_original(d)
            ${t}$ :: c_recovered, c_original
            ${t}$ :: U(d,d), Vt(d,d), S(d)
            ${t}$ :: rmsd

            integer :: i, j
            ${t}$ :: r1
            real(${k}$), parameter :: ${k}$tol = 100 * epsilon(one_${k}$)

            R_recovered = zero_${s}$
            t_recovered = zero_${s}$
            c_recovered = zero_${s}$
            U = zero_${s}$
            S = zero_${k}$
            Vt = zero_${s}$
            rmsd = zero_${k}$

            ! Random reference points Q
            call random_number(Q_original)

            ! Random proper rotation matrix R_original constructed via SVD: R = U * V^T
            call random_number(R_original)
            call svd(R_original, S, U, Vt)
            do i = 1,d
                do j = 1,d
                    R_original(i,j) = stdlib_dot_product_kahan(U(i,:), Vt(:, j))
                end do
            end do

            ! Random scale and translation
            call random_number(c_original)
            call random_number(t_original)

            ! Construct P = c*R*Q + t
            do j = 1, N
                do i = 1, d
                    P_original(i,j) = c_original * &
                        stdlib_dot_product_kahan(R_original(i,:), Q_original(:,j)) + &
                        t_original(i)
                end do
            end do

            ! Call Kabsch–Umeyama
            call kabsch_umeyama(P_original, Q_original, R_recovered, t_recovered, c_recovered, rmsd)

            ! Apply recovered transform
            do j = 1, N
                do i = 1, d
                    P_recovered(i,j) = c_recovered * &
                        stdlib_dot_product_kahan(R_recovered(i,:), Q_original(:,j)) + &
                        t_recovered(i)
                end do
            end do

            call check(error, all_close(P_recovered, P_original, abs_tol = ${k}$tol), .true.)
            if (allocated(error)) return
            call check(error, is_close(rmsd, zero_${k}$, abs_tol = ${k}$tol), .true.)
            if(allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_kabsch_umeyama_complex(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k, t, s in (C_KINDS_TYPES)
        block
            integer, parameter :: d = 3, N = 8
            ${t}$ :: P_original(d, N), Q_original(d, N), P_recovered(d, N)
            ${t}$ :: R_recovered(d, d), R_original(d, d)
            ${t}$ :: t_recovered(d), t_original(d)
            ${t}$ :: c_recovered, c_original
            ${t}$ :: U(d,d), Vt(d,d)
            real(${k}$) :: S(d)
            real(${k}$) :: rmsd

            integer :: i, j
            real(${k}$) :: r1, r2
            real(${k}$), parameter :: ${k}$tol = 100 * epsilon(one_${k}$)

            R_recovered = zero_${s}$
            t_recovered = zero_${s}$
            c_recovered = zero_${s}$
            U = zero_${s}$
            S = zero_${k}$
            Vt = zero_${s}$
            rmsd = zero_${k}$

            ! Random complex reference points Q
            do j = 1, N
                do i = 1, d
                    call random_number(r1)
                    call random_number(r2)
                    Q_original(i,j) = cmplx(r1, r2, kind=${k}$)
                end do
            end do

            ! Random complex unitary matrix R_original
            ! Constructed via SVD: R = U * V^H
            do i = 1, d
                do j = 1, d
                    call random_number(r1)
                    call random_number(r2)
                    R_original(i,j) = cmplx(r1, r2, kind=${k}$)
                end do
            end do
            call svd(R_original, S, U, Vt)
            do i = 1,d
                do j = 1,d
                    R_original(i,j) = stdlib_dot_product_kahan(conjg(U(i,:)), Vt(:, j))
                end do
            end do

            ! Random scale and translation
            call random_number(r1)
            call random_number(r2)
            c_original = cmplx(r1,r2, kind=${k}$)

            do i = 1, d
                call random_number(r1)
                call random_number(r2)
                t_original(i) = cmplx(r1, r2, kind=${k}$)
            end do

            ! Construct P = c*R*Q + t
            do j = 1, N
                do i = 1, d
                    P_original(i,j) = c_original * &
                        stdlib_dot_product_kahan(conjg(R_original(i,:)), Q_original(:,j)) + &
                        t_original(i)
                end do
            end do

            ! Call Kabsch–Umeyama
            call kabsch_umeyama(P_original, Q_original, R_recovered, t_recovered, c_recovered, rmsd)

            ! Apply recovered transform
            do j = 1, N
                do i = 1, d
                    P_recovered(i,j) = c_recovered * &
                        stdlib_dot_product_kahan(conjg(R_recovered(i,:)), Q_original(:,j)) + &
                        t_recovered(i)
                end do
            end do

            call check(error, all_close(P_recovered, P_original, abs_tol = ${k}$tol), .true.)
            if (allocated(error)) return
            call check(error, is_close(rmsd, zero_${k}$, abs_tol = ${k}$tol), .true.)
            if(allocated(error)) return
        end block
        #:endfor
    end subroutine

end module


program tester
    use, intrinsic :: iso_fortran_env, only : error_unit
    use testdrive, only : run_testsuite, new_testsuite, testsuite_type
    use test_kabsch_umeyama, only : collect_suite
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
        new_testsuite("kabsch_umeyama", collect_suite) &
        ]

    do is = 1, size(testsuites)
        write(error_unit, fmt) "Testing:", testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write(error_unit, '(i0, 1x, a)') stat, "test(s) failed!"
        error stop
    end if
end program
